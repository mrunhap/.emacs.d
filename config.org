#+STARTUP: overview
#+PROPERTY: header-args :tangle init.el :mkdirp yes

* Early init

early-init.el is run before init.el,
- before package initialization, and
- before UI initialization

Defer garbage collection further back in the startup process.
#+begin_src emacs-lisp :tangle early-init.el
;;; early-init.el -*- lexical-binding: t; -*-
(setq gc-cons-threshold most-positive-fixnum)
#+end_src

In Emacs 27+, package initialization occurs before =user-init-file= is
loaded, but after =early-init-file=.
#+begin_src emacs-lisp :tangle early-init.el
(setq package-enable-at-startup nil
      package-quickstart nil
      load-prefer-newer t)
#+end_src

=file-remote-p= relies on an entry in =file-name-handler-alist=
(autoloaded by tramp.el) to detect remote paths, which causes tramp
to be loaded.
Set =file-name-handler-alist= to nil at startup for a noteable boost
in startup performance and restore after startup or tramp won't
loaded and project.el won't remember remote projects.
#+begin_src emacs-lisp :tangle early-init.el
(unless (or (daemonp) noninteractive)
  (let ((old-file-name-handler-alist file-name-handler-alist))
    ;; `file-name-handler-alist' is consulted on each `require', `load' and
    ;; various path/io functions. You get a minor speed up by unsetting this.
    ;; Some warning, however: this could cause problems on builds of Emacs where
    ;; its site lisp files aren't byte-compiled and we're forced to load the
    ;; *.el.gz files (e.g. on Alpine).
    (setq-default file-name-handler-alist nil)
    ;; ...but restore `file-name-handler-alist' later, because it is needed for
    ;; handling encrypted or compressed files, among other things.
    (defun my/reset-file-handler-alist ()
      (setq file-name-handler-alist
            ;; Merge instead of overwrite because there may have bene changes to
            ;; `file-name-handler-alist' since startup we want to preserve.
            (delete-dups (append file-name-handler-alist
                                 old-file-name-handler-alist))))
    (add-hook 'emacs-startup-hook #'my/reset-file-handler-alist 101))

  ;; 别他妈闪来闪去的了好不好啊
  (setq-default inhibit-redisplay t
                inhibit-message t)
  (add-hook 'window-setup-hook
            (lambda ()
              (setq-default inhibit-redisplay nil
                            inhibit-message nil)
              (redisplay))))
#+end_src

Prevent the glimpse of un-styled Emacs by disabling these UI elements early.
#+begin_src emacs-lisp :tangle early-init.el
(push '(menu-bar-lines . 0) default-frame-alist)
(push '(tool-bar-lines . 0) default-frame-alist)
(push '(vertical-scroll-bars) default-frame-alist)
#+end_src

Disable native compilation.
#+begin_src emacs-lisp :tangle early-init.el
(setq  native-comp-jit-compilation nil)
#+end_src


#+begin_src elisp :tangle early-init.el
(setq frame-inhibit-implied-resize t)
#+end_src

* Better Defaults
** Use lexical binding

#+begin_src emacs-lisp
;;; -*- lexical-binding: t -*-
#+end_src

** Discard the custom file

#+begin_src elisp
(setq-default lexical-binding t)
(setq custom-file (expand-file-name "custom.el" user-emacs-directory))
(when (and (file-exists-p custom-file) (file-readable-p custom-file))
  (load custom-file :no-error :no-message))
#+end_src

** Const & Var

#+begin_src emacs-lisp
(defconst eat/macp (eq system-type 'darwin))
(defconst eat/linuxp (eq system-type 'gnu/linux))
(defconst eat/winp (eq system-type 'windows-nt))
(defconst my/executable-find-ugrep (executable-find "ugrep"))
(defconst my/executable-find-rg (executable-find "rg"))
#+end_src

** Funcs
*** Delete things(don’t send to kill ring
#+begin_src emacs-lisp
(defun eat/delete-to-the-begining ()
  (interactive)
  (delete-region (point-min) (point)))

(defun eat/delete-to-the-end ()
  (interactive)
  (delete-region (point) (point-max)))

(defun eat/delete-whole-buffer ()
  (interactive)
  (delete-region (point-min) (point-max)))

(defun eat/delete-this-file ()
  "Delete the current file, and kill the buffer."
  (interactive)
  (unless (buffer-file-name)
    (error "No file is currently being edited"))
  (when (yes-or-no-p (format "Really delete '%s'?"
                             (file-name-nondirectory buffer-file-name)))
    (delete-file (buffer-file-name))
    (kill-this-buffer)))
#+end_src
*** Convert timestamp
#+begin_src emacs-lisp
(defun eat/timestamp (begin end)
  "Convert the selected region (a timestamp in seconds) to a formatted time string."
  (interactive "r")
  (let* ((timestamp-str (buffer-substring-no-properties begin end))
         (timestamp (string-to-number timestamp-str))
         (formatted-time (format-time-string "%Y-%m-%d %H:%M:%S" (seconds-to-time timestamp))))
    (message "%s" formatted-time)))
#+end_src
*** Adjust opacity
#+begin_src emacs-lisp
(defun eat/adjust-opacity (frame incr)
  "Adjust the background opacity of FRAME by increment INCR."
  (unless (display-graphic-p frame)
    (error "Cannot adjust opacity of this frame"))
  (let* ((oldalpha (or (frame-parameter frame 'alpha-background) 100))
         (oldalpha (if (listp oldalpha) (car oldalpha) oldalpha))
         (newalpha (+ incr oldalpha)))
    (when (and (<= frame-alpha-lower-limit newalpha) (>= 100 newalpha))
      (modify-frame-parameters frame (list (cons 'alpha-background newalpha))))))
(global-set-key (kbd "M-C-8") (lambda () (interactive) (eat/adjust-opacity nil -2)))
(global-set-key (kbd "M-C-9") (lambda () (interactive) (eat/adjust-opacity nil 2)))
(global-set-key (kbd "M-C-7") (lambda () (interactive) (modify-frame-parameters nil `((alpha-background . 100)))))
#+end_src
*** Load PATH
#+begin_src elisp
;;; Setup PATH
;; https://emacs-china.org/t/emacs-mac-port-profile/2895/29?u=rua
;; NOTE: When PATH is changed, run the following command
;; $ sh -c 'printf "%s" "$PATH"' > ~/.path
;; then add this to custom.el
;; (add-hook 'after-init-hook #'eat/getenv-path)
(defun eat/getenv-path()
  (interactive)
  (condition-case err
      (let ((path (with-temp-buffer
                    (insert-file-contents-literally "~/.path")
                    (buffer-string))))
        (setenv "PATH" path)
        (setq exec-path (append (parse-colon-path path) (list exec-directory))))
    (error (warn "%s" (error-message-string err)))))

(if (file-exists-p "~/.path")
    (add-hook 'after-init-hook #'eat/getenv-path)
  (message "%s" "Run '$ sh -c 'printf \"%s\" \"$PATH\"' > ~/.path' in your terminal then M-x eat/getenv-path."))
#+end_src

*** Misc
#+begin_src emacs-lisp
(defun get-string-from-file (filePath)
  "Return file content as string."
  (with-temp-buffer
    (insert-file-contents filePath)
    (buffer-string)))

(defun eat/quit ()
  "Delete current window switch to prevous buffer."
  (interactive)
  (if (> (seq-length (window-list (selected-frame))) 1)
      (delete-window)
    (previous-buffer)))

;; http://emacsredux.com/blog/2013/05/22/smarter-navigation-to-the-beginning-of-a-line/
(defun smarter-move-beginning-of-line (arg)
  "Move point back to indentation of beginning of line.

Move point to the first non-whitespace character on this line.
If point is already there, move to the beginning of the line.
Effectively toggle between the first non-whitespace character and
the beginning of the line.

If ARG is not nil or 1, move forward ARG - 1 lines first.  If
point reaches the beginning or end of the buffer, stop there."
  (interactive "^p")
  (setq arg (or arg 1))

  ;; Move lines first
  (when (/= arg 1)
    (let ((line-move-visual nil))
      (forward-line (1- arg))))

  (let ((orig-point (point)))
    (back-to-indentation)
    (when (= orig-point (point))
      (move-beginning-of-line 1))))
(global-set-key [remap move-beginning-of-line] #'smarter-move-beginning-of-line)
#+end_src
*** Window
#+begin_src emacs-lisp
;; When splitting window, show (other-buffer) in the new window

(defun split-window-func-with-other-buffer (split-function)
  (lambda (&optional arg)
    "Split this window and switch to the new window unless ARG is provided."
    (interactive "P")
    (funcall split-function)
    (let ((target-window (next-window)))
      (set-window-buffer target-window (other-buffer))
      (unless arg
        (select-window target-window)))))

(keymap-global-set "C-x 2" (split-window-func-with-other-buffer 'split-window-vertically))
(keymap-global-set "C-x 3" (split-window-func-with-other-buffer 'split-window-horizontally))

(defun sanityinc/toggle-delete-other-windows ()
  "Delete other windows in frame if any, or restore previous window config."
  (interactive)
  (if (and winner-mode
           (equal (selected-window) (next-window)))
      (winner-undo)
    (delete-other-windows)))

(keymap-global-set "C-x 1" 'sanityinc/toggle-delete-other-windows)

(defun split-window-horizontally-instead ()
  "Kill any other windows and re-split such that the current window is on the top half of the frame."
  (interactive)
  (let ((other-buffer (and (next-window) (window-buffer (next-window)))))
    (delete-other-windows)
    (split-window-horizontally)
    (when other-buffer
      (set-window-buffer (next-window) other-buffer))))

(defun split-window-vertically-instead ()
  "Kill any other windows and re-split such that the current window is on the left half of the frame."
  (interactive)
  (let ((other-buffer (and (next-window) (window-buffer (next-window)))))
    (delete-other-windows)
    (split-window-vertically)
    (when other-buffer
      (set-window-buffer (next-window) other-buffer))))

(keymap-global-set "C-x |" 'split-window-horizontally-instead)
(keymap-global-set "C-x _" 'split-window-vertically-instead)
#+end_src
** Performance
*** Garbge collection
#+begin_src emacs-lisp
(let ((normal-gc-cons-threshold (* 20 1024 1024))
      (init-gc-cons-threshold (* 128 1024 1024)))
  (setq gc-cons-threshold init-gc-cons-threshold)
  (add-hook 'emacs-startup-hook
            (lambda () (setq gc-cons-threshold normal-gc-cons-threshold))))

;; GC automatically while unfocusing the frame
(add-function :after after-focus-change-function
              (lambda ()
                (unless (frame-focus-state)
                  (garbage-collect))))
#+end_src
*** Custom hook
#+begin_src emacs-lisp
;; Hooks split from doom-emacs
(defvar on-first-input-hook nil
  "Transient hooks run before the first user input.")
(put 'on-first-input-hook 'permanent-local t)

(defvar on-first-file-hook nil
  "Transient hooks run before the first interactively opened file.")
(put 'on-first-file-hook 'permanent-local t)

(defvar on-first-buffer-hook nil
  "Transient hooks run before the first interactively opened buffer.")
(put 'on-first-buffer-hook 'permanent-local t)

(defvar on-init-ui-hook nil
  "List of hooks to run when the UI has been initialized.")

(defun on-run-hook-on (hook-var trigger-hooks)
  "Configure HOOK-VAR to be invoked exactly once when any of the TRIGGER-HOOKS
are invoked *after* Emacs has initialized (to reduce false positives). Once
HOOK-VAR is triggered, it is reset to nil.

HOOK-VAR is a quoted hook.
TRIGGER-HOOK is a list of quoted hooks and/or sharp-quoted functions."
  (dolist (hook trigger-hooks)
    (let ((fn (intern (format "%s-init-on-%s-h" hook-var hook))))
      (fset
       fn (lambda (&rest _)
            ;; Only trigger this after Emacs has initialized.
            (when (and after-init-time
                       (or (daemonp)
                           ;; In some cases, hooks may be lexically unset to
                           ;; inhibit them during expensive batch operations on
                           ;; buffers (such as when processing buffers
                           ;; internally). In these cases we should assume this
                           ;; hook wasn't invoked interactively.
                           (and (boundp hook)
                                (symbol-value hook))))
              (run-hooks hook-var)
              (set hook-var nil))))
      (cond ((daemonp)
             ;; In a daemon session we don't need all these lazy loading
             ;; shenanigans. Just load everything immediately.
             (add-hook 'after-init-hook fn 'append))
            ((eq hook 'find-file-hook)
             ;; Advise `after-find-file' instead of using `find-file-hook'
             ;; because the latter is triggered too late (after the file has
             ;; opened and modes are all set up).
             (advice-add 'after-find-file :before fn '((depth . -101))))
            ((add-hook hook fn -101)))
      fn)))

(defun on-init-ui-h (&optional _)
  "Initialize user interface by applying its hooks.

These should be done as late as possible, as to avoid/minimize prematurely
triggering hooks during startup."
  (run-hooks 'on-init-ui-hook)

  ;; Add trigger hooks to `on-first-buffer-hook'.
  (on-run-hook-on 'on-first-buffer-hook '(window-buffer-change-functions server-visit-hook))

  ;; Only execute this function once.
  (remove-hook 'window-buffer-change-functions #'on-init-ui-h))

;; Initialize UI as late as possible. `window-buffer-change-functions' runs
;; once, when the scratch/dashboard buffer is first displayed.
(add-hook 'window-buffer-change-functions #'on-init-ui-h -100)

(unless noninteractive
  (on-run-hook-on 'on-first-buffer-hook '(find-file-hook on-switch-buffer-hook))
  (on-run-hook-on 'on-first-file-hook   '(find-file-hook dired-initial-position-hook))
  (on-run-hook-on 'on-first-input-hook  '(pre-command-hook)))
#+end_src
*** Show startup time
#+begin_src emacs-lisp
(defun eat/show-startup-time ()
  "Print startup time."
  (message
   "Emacs loaded in %s with %d garbage collections."
   (format
    "%.2f seconds"
    (float-time
     (time-subtract after-init-time before-init-time)))
   gcs-done))
(add-hook 'emacs-startup-hook #'eat/show-startup-time)
#+end_src
*** Misc
#+begin_src emacs-lisp
;; Don't pass case-insensitive to `auto-mode-alist'
(setq auto-mode-case-fold nil)

;; Don't ping things that look like domain names.
(setq ffap-machine-p-known 'reject)
#+end_src
*** Process tuning
#+begin_src emacs-lisp
(setq read-process-output-max (* 4 1024 1024))
(setq process-adaptive-read-buffering nil)
#+end_src
*** Long line
#+begin_src emacs-lisp
(add-hook 'on-first-file-hook #'global-so-long-mode)

;; https://emacs-china.org/t/topic/25811/9?u=rua
(setq-default bidi-display-reordering 'left-to-right)
(setq bidi-inhibit-bpa t
      long-line-threshold 1000
      large-hscroll-threshold 1000
      syntax-wholeline-max 1000)
#+end_src
*** Don’t compact font
=inhibit-compacting-font-caches= 是 Emacs 中的一个变量，用来控制字体缓存
的压缩。这个变量的主要目的是优化 Emacs 在处理大量或复杂字体时的性能。

当 =inhibit-compacting-font-caches= 设置为 nil（默认值）时，Emacs 会
尝试压缩字体缓存。这意味着在处理大量或复杂字体的情况下，Emacs 会努
力节省内存空间，但可能会以牺牲一些性能为代价。

当 =inhibit-compacting-font-caches= 设置为 t 时，Emacs 将不会压缩字
体缓存。这可以提高处理大量或复杂字体时的性能，尤其是在有较多非
ASCII字符（如中文、日文、韩文等）的情况下。不过，不压缩字体缓存可
能会占用更多的内存。

总的来说，是否设置这个变量取决于你的使用场景。如果你经常处理包含大量复
杂字体的文档，且希望提高性能（尤其是在字体渲染方面），可以考虑将其设置
为 t。如果你不太关心性能或者不经常处理复杂字体，保持默认值 nil 即可。

#+begin_src emacs-lisp
;; Don’t compact font caches during GC.
(setq inhibit-compacting-font-caches t)
#+end_src
** Better default
*** Some basic config
#+begin_src emacs-lisp
(setq inhibit-startup-screen t
      ;; don't load default.el
      inhibit-default-init t
      ;; Don't use prog-mode an stratup
      initial-major-mode 'fundamental-mode
      ;; no client startup messages
      server-client-instructions nil)

;; Shut up!
(defun display-startup-echo-area-message() nil)

(setq require-final-newline t
      ;; Echo current unfinished command immediately.
      echo-keystrokes 0.1
      ;; Disable "You can run the command balabala..."
      suggest-key-bindings nil
      ;; 默认软折行是根据空格来的，但是中文句子没有空格，所以需要开启
      word-wrap-by-category t
      cursor-in-non-selected-windows nil
      visible-cursor t
      warning-suppress-log-types '((comp)) ; Don't display compile warnings
      truncate-partial-width-windows 65 ; Don't truncate lines in a window narrower than 65 chars.
      vc-follow-symlinks t)

(setq ring-bell-function 'ignore
      ;; Press e to edit variable value in help buffer.
      help-enable-variable-value-editing t
      ;; Disable gui box.
      use-file-dialog nil
      use-dialog-box nil
      use-short-answers t
      ;; NOTE 这是干啥的？
      redisplay-skip-fontification-on-input t)

;; Monitors are trending toward wide, rather than tall.
(setq split-width-threshold 160)
(setq split-height-threshold nil)

 ;; NOTE this may cause lsp-bridge-ref buffer didn't show
(setq window-resize-pixelwise nil
      frame-resize-pixelwise t)
#+end_src
*** Completion
#+begin_src emacs-lisp
(setq
 completion-styles '(basic partial-completion)
 completion-category-overrides '((file (styles basic partial-completion))))
#+end_src

*** Minibuffer
#+begin_src emacs-lisp
;; Quit the fucking minibuffer!
(keymap-set minibuffer-local-map "C-g" #'keyboard-quit)

;; Add prompt indicator to `completing-read-multiple'.
;; We display [CRM<separator>], e.g., [CRM,] if the separator is a comma.
(defun crm-indicator (args)
  (cons (format "[CRM%s] %s"
                (replace-regexp-in-string
                 "\\`\\[.*?]\\*\\|\\[.*?]\\*\\'" ""
                 crm-separator)
                (car args))
        (cdr args)))
(advice-add #'completing-read-multiple :filter-args #'crm-indicator)

;; Do not allow the cursor in the minibuffer prompt
(setq minibuffer-prompt-properties
      '(read-only t cursor-intangible t face minibuffer-prompt))
(add-hook 'minibuffer-setup-hook #'cursor-intangible-mode)

;; Emacs 28: Hide commands in M-x which do not work in the current mode.
;; Vertico commands are hidden in normal buffers.
(setq read-extended-command-predicate #'command-completion-default-include-p)

;; Enable recursive minibuffers
(setq enable-recursive-minibuffers t)
#+end_src
*** Frame hooks

Some ui and font config need to add to these hooks to make it works under emacsclient.

#+begin_src elisp
(defvar after-make-console-frame-hooks '()
  "Hooks to run after creating a new TTY frame")
(defvar after-make-window-system-frame-hooks '()
  "Hooks to run after creating a new window-system frame")

(defun run-after-make-frame-hooks (frame)
  "Run configured hooks in response to the newly-created FRAME.
Selectively runs either `after-make-console-frame-hooks' or
`after-make-window-system-frame-hooks'"
  (with-selected-frame frame
    (run-hooks (if window-system
                   'after-make-window-system-frame-hooks
                 'after-make-console-frame-hooks))))

(add-hook 'after-make-frame-functions 'run-after-make-frame-hooks)

(defconst sanityinc/initial-frame (selected-frame)
  "The frame (if any) active during Emacs initialization.")

(add-hook 'after-init-hook
          (lambda () (when sanityinc/initial-frame
                       (run-after-make-frame-hooks sanityinc/initial-frame))))
#+end_src

*** MacOS
#+begin_src elisp
(when eat/macp
  (setq mac-option-modifier 'meta
        mac-command-modifier 'super)
  (global-set-key [(super a)] #'mark-whole-buffer)
  (global-set-key [(super v)] #'yank)
  (global-set-key [(super c)] #'kill-ring-save)
  (global-set-key [(super s)] #'save-buffer)
  (global-set-key [(super w)] #'delete-frame)
  (global-set-key [(super z)] #'undo)
  ;; `save-buffers-kill-emacs' will shutdown emacs daemon.
  (global-set-key [(super q)] #'save-buffers-kill-terminal)

  (setq ns-use-native-fullscreen nil
        ;; Render thinner fonts
        ns-use-thin-smoothing t
        ;; Don't open a file in a new frame
        ns-pop-up-frames nil)
  (set-frame-parameter nil 'fullscreen 'maximized))
#+end_src

*** Linux
#+begin_src elisp
(setq-default
 x-underline-at-descent-line t
 ;; Don't use Fcitx5 in Emacs in PGTK build.
 pgtk-use-im-context-on-new-connection nil
 ;; Avoid breakage of childframes.
 x-gtk-resize-child-frames 'resize-mode
 ;; Don't use GTK+ tooltip.
 x-gtk-use-system-tooltips nil)
#+end_src

*** Keybinding
#+begin_src emacs-lisp
(keymap-global-set "C-h C-k" #'describe-keymap)
;; this will stuck emacs
(keymap-global-unset "C-h h")

(when (display-graphic-p)
  (global-unset-key (kbd "C-z"))
  (global-unset-key (kbd "C-x C-z")))
#+end_src
*** Edit
**** Scroll
#+begin_src emacs-lisp
(setq hscroll-step 1
      hscroll-margin 2
      ;; The nano style for truncated long lines.
      auto-hscroll-mode 'current-line
      scroll-margin 0
      scroll-conservatively 101
      scroll-preserve-screen-position t
      auto-window-vscroll nil
      ;; Use shift + mouse wheel to scrll horizontally.
      mouse-wheel-scroll-amount '(2 ((shift) . hscroll))
      mouse-wheel-scroll-amount-horizontal 2)

(add-hook 'after-init-hook (lambda () (pixel-scroll-precision-mode)))

(setq pixel-scroll-precision-interpolate-page t)

(defun +pixel-scroll-interpolate-down (&optional lines)
  (interactive)
  (if lines
      (pixel-scroll-precision-interpolate (* -1 lines (pixel-line-height)))
    (pixel-scroll-interpolate-down)))

(defun +pixel-scroll-interpolate-up (&optional lines)
  (interactive)
  (if lines
      (pixel-scroll-precision-interpolate (* lines (pixel-line-height))))
  (pixel-scroll-interpolate-up))

(defalias 'scroll-up-command '+pixel-scroll-interpolate-down)
(defalias 'scroll-down-command '+pixel-scroll-interpolate-up)
#+end_src
**** Auto save
#+begin_src emacs-lisp
;; Don't generate backups or lockfiles. While auto-save maintains a copy so long
;; as a buffer is unsaved, backups create copies once, when the file is first
;; written, and never again until it is killed and reopened. This is better
;; suited to version control, and I don't want world-readable copies of
;; potentially sensitive material floating around our filesystem.
(setq create-lockfiles nil
      make-backup-files nil
      auto-save-default nil)

;; Most of time I will do save manually.
(setq auto-save-visited-interval 10)
(add-hook 'after-init-hook #'auto-save-visited-mode)
#+end_src
**** Use four spaces instead of tab
#+begin_src emacs-lisp
;; indent with whitespace by default
(setq-default tab-width 4
              indent-tabs-mode nil)
#+end_src
**** Use utf-8 as default coding system
#+begin_src emacs-lisp
;; Contrary to what many Emacs users have in their configs, you don't need
;; more than this to make UTF-8 the default coding system:
(set-language-environment "UTF-8")
#+end_src
**** Delete whitespace after save
#+begin_src emacs-lisp
(add-hook 'before-save-hook #'delete-trailing-whitespace)
#+end_src
**** Query & Replace
The problem of the default query-replace UI is when you accidently
press a key that's not in query-replace-map, the session is
terminated. This makes it feel fragile.

Here's an advice fixing it. When you press a non query-replace-map
key, it opens the help info.

#+begin_src emacs-lisp
(define-advice perform-replace (:around (fn &rest args) dont-exit-on-anykey)
  "Don't exit replace for anykey that's not in `query-replace-map'."
  (cl-letf* ((lookup-key-orig
              (symbol-function 'lookup-key))
             ((symbol-function 'lookup-key)
              (lambda (map key &optional accept-default)
                (or (apply lookup-key-orig map key accept-default)
                    (when (eq map query-replace-map) 'help)))))
    (apply fn args)))
#+end_src
Stole from https://github.com/astoff/isearch-mb/wiki
*** UI
**** Font
***** Var
#+begin_src emacs-lisp
(defvar eat/fonts-default        '("Monaco" "Cascadia Code" "Menlo" "Source Code Pro"))
(defvar eat/fonts-variable-pitch '("Bookerly" "Cardo" "Times New Roman" "DejaVu Sans"))
(defvar eat/fonts-cjk            '("LXGW WenKai" "WenQuanYi Micro Hei" "Microsoft Yahei"))
(defvar eat/fonts-unicode        '("Symbola"))
(defvar eat/fonts-emoji          '("Apple Color Emoji" "Segoe UI Symbol" "Noto Color Emoji"))
(defvar eat/font-size-default   13)
#+end_src
***** Setup
#+begin_src emacs-lisp
(defun font-installed-p (font-list)
  (let ((font-installed nil))
    (catch 'foo
      (dolist (font font-list)
        (when (find-font (font-spec :name font))
          (setq font-installed font)
          (throw 'foo t))))
    font-installed))

(defvar eat/font-default        (font-installed-p eat/fonts-default))
(defvar eat/font-variable-pitch (font-installed-p eat/fonts-variable-pitch))
(defvar eat/font-cjk            (font-installed-p eat/fonts-cjk))
(defvar eat/font-unicode        (font-installed-p eat/fonts-unicode))
(defvar eat/font-emoji          (font-installed-p eat/fonts-emoji))

(defun eat/setup-font ()
  (set-face-attribute 'default nil :height (* 10 eat/font-size-default))
  (when eat/font-default
    (set-face-attribute 'default     nil :family eat/font-default)
    (set-face-attribute 'fixed-pitch nil :font eat/font-default))
  (when eat/font-variable-pitch
    (set-face-font 'variable-pitch eat/font-variable-pitch))
  (when eat/font-unicode
    (set-fontset-font t 'unicode eat/font-unicode))
  (when eat/font-emoji
    (set-fontset-font t 'emoji   eat/font-emoji))
  (when eat/font-cjk
    (set-fontset-font t 'kana     eat/font-cjk)
    (set-fontset-font t 'han      eat/font-cjk)
    (set-fontset-font t 'cjk-misc eat/font-cjk)))
(add-hook 'after-make-window-system-frame-hooks #'eat/setup-font)
#+end_src
***** Rescale
#+begin_src emacs-lisp
(defvar eat/font-rescale-alist
  `((,eat/font-cjk     . 0.95)
    (,eat/font-emoji   . 0.9)
    (,eat/font-unicode . 0.95))
  "A list of font names that should be rescaled.")

(defun eat/rescale-font ()
  (interactive)
  (dolist (setting eat/font-rescale-alist)
    (when (car setting)
      (setf (alist-get (car setting)
                       face-font-rescale-alist nil nil #'equal)
            (cdr setting)))))
(add-hook 'after-make-window-system-frame-hooks #'eat/rescale-font)
#+end_src
***** Util funcs
#+begin_src emacs-lisp
(defun eat/fixed-pitch-setup ()
  (interactive)
  (setq buffer-face-mode-face '(:family "Sarasa Mono SC"))
  (buffer-face-mode +1))
#+end_src
**** Theme
***** Add a hook after load theme
#+begin_src emacs-lisp
(defvar eat/theme 'pale
  "Default theme.")

(defvar after-load-theme-hook nil
  "Hooks run after `load-theme'.")

(defun eat/load-theme (f theme &optional no-confirm no-enable &rest args)
  (interactive
   (list
    (intern (completing-read "Theme: "
                             (mapcar #'symbol-name
				                     (custom-available-themes))))))
  (dolist (theme custom-enabled-themes)
    (disable-theme theme))
  (if (featurep (intern (format "%s-theme" theme)))
      (enable-theme theme)
    (apply f theme t no-enable args))
  (run-hooks 'after-load-theme-hook))
(advice-add 'load-theme :around #'eat/load-theme)

(add-hook 'after-make-window-system-frame-hooks #'(lambda () (load-theme eat/theme)))
#+end_src
***** Modern mode line
#+begin_src emacs-lisp
;; (defun i-dont-want-see-box ()
;;   "Remove box attirbute from mode-line-active and use it's color as background."
;;   (let ((box-color (plist-get (face-attribute 'mode-line-active :box) :color)))
;;     (when box-color
;;       (set-face-attribute 'mode-line nil :background box-color :box nil)
;;       (set-face-attribute 'mode-line-active nil :background box-color :box nil)
;;       (set-face-attribute 'mode-line-inactive nil :box nil)
;;       (set-face-attribute 'mode-line-inactive nil :box nil)
;;       (set-face-attribute 'tab-bar nil :box nil)
;;       (set-face-attribute 'tab-bar-tab nil :box nil)
;;       (set-face-attribute 'tab-bar-tab-inactive nil :box nil))))

;; (when (display-graphic-p)
;;   (add-to-list 'mode-line-format (list (propertize "  " 'display '(raise 0.3))
;;                                        (propertize " " 'display '(raise -0.3)))
;;                t)
;;   (add-hook 'after-load-theme-hook #'i-dont-want-see-box)
;;   )
#+end_src

** Built-in packages
*** package.el
#+begin_src emacs-lisp
(setq package-archives '(("gnu"    . "http://mirrors.tuna.tsinghua.edu.cn/elpa/gnu/")
			             ("nongnu" . "http://mirrors.tuna.tsinghua.edu.cn/elpa/nongnu/")
                         ("melpa"  . "http://mirrors.tuna.tsinghua.edu.cn/elpa/melpa/"))
      package-quickstart t)
(package-activate-all)

(defmacro eat/pkg (package &optional vc)
  `(unless (package-installed-p ',package)
     (unless (memq ',package package-archive-contents)
       (package-refresh-contents))
     (if ,vc (package-vc-install ,vc)
       (package-install ',package))))
#+end_src
*** browse-url
#+begin_src emacs-lisp
(setq browse-url-generic-program
      (or (executable-find "firefox")
          (when eat/macp "open")
          (when eat/linuxp "xdg-open")))
#+end_src
*** tab-bar
NOTE do not bind =tab-bar-switch-to-prev-tab= and
=tab-bar-switch-to-next-tab= to =M-[= or =M-]=, it will make emacs have some
bug to auto insert characters after you type everytime.

See =tab-prefix-map= to custom key bindings for tab-bar, default is =C-x t=.

#+begin_src emacs-lisp
(defun tab-bar-format-menu-bar ()
  "Produce the Menu button for the tab bar that shows the menu bar."
  `((menu-bar menu-item
              (format " %s  "
                      (nerd-icons-sucicon "nf-custom-emacs"
                                          :face '(:inherit nerd-icons-purple)))
              tab-bar-menu-bar :help "Menu Bar")))

(defun eat/bar-image ()
  (when (and (display-graphic-p) (image-type-available-p 'pbm))
    (propertize
     " " 'display
     (ignore-errors
       (create-image
        ;; 20 for `dirvish-header-line-height'
        (concat (format "P1\n%i %i\n" 2 30) (make-string (* 2 30) ?1) "\n")
        'pbm t :foreground (face-background 'highlight) :ascent 'center)))))

(setq tab-bar-new-tab-choice 'ibuffer
      tab-bar-close-last-tab-choice 'tab-bar-mode-disable
      tab-bar-tab-hints nil
      tab-bar-close-button-show nil
      tab-bar-separator ""
      tab-bar-format '(tab-bar-format-menu-bar
                       tab-bar-format-tabs)
      ;; NOTE 如果要用到很多 tab 导致 tab 换行的话就把这个设置为 t
      tab-bar-auto-width nil
      tab-bar-tab-name-format-function
      (lambda (tab i) "Center, taller, better, stronger xD."
        (let* ((current-tab-p (eq (car tab) 'current-tab))
               (bar (when current-tab-p (eat/bar-image)))
               (name (string-trim (alist-get 'name tab)))
               (space-to-add (max 0 (- tab-bar-tab-name-truncated-max (length name))))
               (left-padding (/ space-to-add 2))
               (right-padding (- space-to-add left-padding)))
          (concat
           bar
           (propertize (concat ;; (propertize " " 'display '(raise 0.3))
                               (make-string left-padding ?\s)
                               name
                               (make-string right-padding ?\s)
                               ;; (propertize " " 'display '(raise -0.3))
                               )
                       'face (funcall tab-bar-tab-face-function tab)))))
      tab-bar-tab-name-function
      (lambda nil "Use project as tab name."
        (let ((dir (expand-file-name
                    (or (if (and (fboundp 'project-root) (project-current))
                            (project-root (project-current)))
                        default-directory))))
          (or
           (and dir
                (let ((name (substring dir (1+ (string-match "/[^/]+/$" dir)) -1)))
                  (truncate-string-to-width name tab-bar-tab-name-truncated-max nil ? )))
           (buffer-name)))))

(with-eval-after-load 'tab-bar
  (tab-bar-history-mode 1))
#+end_src
*** grep
#+begin_src emacs-lisp
  (autoload 'grep-apply-setting "grep")
  (cond
   (my/executable-find-ugrep
    (grep-apply-setting 'grep-command "ugrep -0In -e ")
    (grep-apply-setting 'grep-template "ugrep -0In -e <R> <D>")
    (grep-apply-setting 'grep-find-command '("ugrep -0Inr -e ''" . 30))
    (grep-apply-setting 'grep-find-template "ugrep <C> -0Inr -e <R> <D>"))
   (my/executable-find-rg
    (grep-apply-setting 'grep-command "rg --null -nH --no-heading -e ")
    (grep-apply-setting 'grep-template "rg --null --no-heading -g '!*/' -e <R> <D>")
    (grep-apply-setting 'grep-find-command '("rg --null -nH --no-heading -e ''" . 38))
    (grep-apply-setting 'grep-find-template "rg --null -nH --no-heading -e <R> <D>")))
#+end_src
*** dired
#+begin_src emacs-lisp
(setq mouse-drag-and-drop-region t
      mouse-drag-and-drop-region-cross-program t)

(setq dired-mouse-drag-files t
      dired-dwim-target t
      dired-kill-when-opening-new-dired-buffer t
      dired-auto-revert-buffer t)

(with-eval-after-load 'dired
  (setq dired-listing-switches
        "-l --almost-all --human-readable --time-style=long-iso --group-directories-first --no-group")
  (keymap-set dired-mode-map "C-c C-p" #'wdired-change-to-wdired-mode)
  (define-key dired-mode-map (kbd "h") #'dired-up-directory)
  (define-key dired-mode-map [mouse-2] #'dired-find-file))
#+end_src
*** recentf
#+begin_src emacs-lisp
(add-hook 'on-first-input-hook #'recentf-mode)
(setq recentf-max-saved-items 1000
      recentf-exclude `(,tramp-file-name-regexp
                        "COMMIT_EDITMSG"))
(keymap-global-set "C-x C-r" #'recentf-open-files)
#+end_src

*** webjump
#+begin_src emacs-lisp
(keymap-global-set "C-x C-/" #'webjump)
(setq webjump-sites
      '(("Emacs Wiki" . [simple-query "www.emacswiki.org" "www.emacswiki.org/cgi-bin/wiki/" #1=""])
        ("Emacs China" . "emacs-china.org")
        ("Emacs Reddit" . "www.reddit.com/r/emacs/")
        ("Emacs News" . "sachachua.com/blog/category/emacs-news/")
        ("Github" . [simple-query "github.com" "github.com/search?q=" #1#])
        ("Google" . [simple-query "google.com" "google.com/search?q=" #1#])
        ("Kagi" . [simple-query "kagi.com" "kagi.com/search?q=" #1#])
        ("Youtube" . [simple-query "youtube.com" "youtube.com/results?search_query=" #1#])
        ("Google Groups" . [simple-query "groups.google.com" "groups.google.com/groups?q=" #1#])
        ("stackoverflow" . [simple-query "stackoverflow.com" "stackoverflow.com/search?q=" #1#])
        ("Wikipedia" . [simple-query "wikipedia.org" "wikipedia.org/wiki/" #1#])))
#+end_src

*** repeat
#+begin_src emacs-lisp
(setq repeat-mode t
      repeat-keep-prefix t
      repeat-exit-timeout 3
      repeat-exit-key (kbd "RET"))
#+end_src

*** project
#+begin_src elisp
(setq compilation-scroll-output 'first-error)

(with-eval-after-load 'project
  ;; use fd in `project-find-file'
  (defun eat/project-files-in-directory (dir)
    "Use `fd' to list files in DIR."
    (let* ((default-directory dir)
           (localdir (file-local-name (expand-file-name dir)))
           (command (format "fd -c never -H -t f -0 . %s" localdir)))
      (project--remote-file-names
       (sort (split-string (shell-command-to-string command) "\0" t)
             #'string<))))
  (when (executable-find "fd")
    (cl-defmethod project-files ((project (head local)) &optional dirs)
      "Override `project-files' to use `fd' in local projects."
      (mapcan #'eat/project-files-in-directory
              (or dirs (list (project-root project))))))
  (setq project-vc-ignores '("target/" "bin/" "obj/")
        project-vc-extra-root-markers '(".project"
                                        "go.mod"
                                        "Cargo.toml"
                                        "project.clj"
                                        "pom.xml"
                                        "package.json"
                                        "Makefile"
                                        "README.org"
                                        "README.md")))
#+end_src

*** xref
Must set before consult.
Use control-left-click to jump to defintions.

Set cursor pos to mouse pos if they are not in same pos.
#+begin_src elisp
(add-hook 'xref-after-return-hook #'recenter)
(add-hook 'xref-after-jump-hook #'recenter)

(keymap-global-unset "C-<down-mouse-1>")
(keymap-global-set "C-<mouse-1>" #'xref-find-definitions-at-mouse)

(setq xref-prompt-for-identifier nil
      xref-show-xrefs-function #'xref-show-definitions-completing-read
      xref-show-definitions-function #'xref-show-definitions-completing-read
      ;; fix massed xref cross multiple project
      xref-history-storage 'xref-window-local-history)

;; Commands such as `project-find-regexp' that rely on Xref can now
;; leverage the power of ugrep or ripgrep.
(cond (my/executable-find-ugrep (setq xref-search-program 'ugrep))
      (my/executable-find-rg (setq xref-search-program 'rg)))
#+end_src

*** tramp

Custom =vc-igrone-dir-regexp= will make porject don’t regconize and save
remote project, and eglot also don’t work under project.

If it’s still feel slow, try =direct-async-process=:
#+begin_example
(add-to-list 'tramp-connection-properties
             (list "/ssh:" "direct-async-process" t))
;; or
(add-to-list 'tramp-connection-properties
             (list (regexp-quote "/ssh:user@host:")
                   "direct-async-process" t))
#+end_example
However it has some [[https://www.gnu.org/software/tramp/#Improving-performance-of-asynchronous-remote-processes-1][limitations]], the most importent is it does not use
tramp-remote-path, so the process may can’t find remote programs.


#+begin_src elisp
(setq
 tramp-terminal-type "tramp"
 ;; Set remote-file-name-inhibit-cache to nil if remote files are not
 ;; independently updated outside TRAMP’s control. That cache cleanup
 ;; will be necessary if the remote directories or files are updated
 ;; independent of TRAMP.
 remote-file-name-inhibit-cache nil
 ;;  Disable file locks. Set remote-file-name-inhibit-locks to t if
 ;;  you know that different Emacs sessions are not modifying the same
 ;;  remote file.
 remote-file-name-inhibit-locks t
 ;; Disable excessive traces.
 tramp-verbose 0
 ;; C-x C-f /ssh:
 tramp-default-method "ssh"
 ;; Bypass TRAMP’s handling of the ControlMaster options, and use your
 ;; own settings in ~/.ssh/config
 tramp-use-connection-share nil
 ;;  speed up complete
 tramp-completion-reread-directory-timeout nil
 tramp-auto-save-directory temporary-file-directory
 )

(defun sudo-find-file (file)
  "Open FILE as root."
  (interactive "FOpen file as root: ")
  (when (file-writable-p file)
    (user-error "File is user writeable, aborting sudo"))
  (find-file (if (file-remote-p file)
                 (concat "/" (file-remote-p file 'method) ":"
                         (file-remote-p file 'user) "@" (file-remote-p file 'host)
                         "|sudo:root@"
                         (file-remote-p file 'host) ":" (file-remote-p file 'localname))
               (concat "/sudo:root@localhost:" file))))
(defun sudo-this-file ()
  "Open the current file as root."
  (interactive)
  (sudo-find-file (file-truename buffer-file-name)))
(keymap-global-set "C-x C-z" #'sudo-this-file)

(with-eval-after-load 'tramp
  ;; ‘Private Directories’ are the settings of the $PATH environment,
  ;; as given in your ‘~/.profile’.  This entry is represented in
  ;; the list by the special value ‘tramp-own-remote-path’.
  (add-to-list 'tramp-remote-path 'tramp-own-remote-path))
#+end_src

*** isearch
#+begin_src emacs-lisp
(setq
 ;; Match count next to the minibuffer prompt
 isearch-lazy-count t
 isearch-lazy-highlight t
 lazy-highlight-buffer t
 ;; Don't be stingy with history; default is to keep just 16 entries
 search-ring-max 200
 regexp-search-ring-max 200
 ;; Record isearch in minibuffer history, so C-x ESC ESC can repeat it.
 isearch-resume-in-command-history t
 ;; M-< and M-> move to the first/last occurrence of the current search string.
 isearch-allow-motion t
 isearch-motion-changes-direction t
 ;; space matches any sequence of characters in a line.
 isearch-regexp-lax-whitespace t
 search-whitespace-regexp ".*?")

(keymap-global-set "C-s" #'isearch-forward-regexp)
(keymap-global-set "C-r" #'isearch-backward-regexp)

(with-eval-after-load "isearch"
  (define-advice isearch-occur (:after (_regexp &optional _nlines))
    "Exit isearch after calling."
    (isearch-exit))

  (keymap-set isearch-mode-map "C-c C-o" #'isearch-occur)
  ;; DEL during isearch should edit the search string, not jump back
  ;; to the previous result
  (keymap-substitute isearch-mode-map #'isearch-delete-chac #'isearch-del-chac)

  ;; steal from
  ;; http://yummymelon.com/devnull/improving-emacs-isearch-usability-with-transient.html
  (transient-define-prefix my/isearch-menu ()
    "isearch Menu"
    [["Edit Search String"
      ("e"
       "Edit the search string (recursive)"
       isearch-edit-string
       :transient nil)
      ("w"
       "Pull next word or character word from buffer"
       isearch-yank-word-or-char
       :transient nil)
      ("s"
       "Pull next symbol or character from buffer"
       isearch-yank-symbol-or-char
       :transient nil)
      ("l"
       "Pull rest of line from buffer"
       isearch-yank-line
       :transient nil)
      ("y"
       "Pull string from kill ring"
       isearch-yank-kill
       :transient nil)
      ("t"
       "Pull thing from buffer"
       isearch-forward-thing-at-point
       :transient nil)]

     ["Replace"
      ("q"
       "Start ‘query-replace’"
       isearch-query-replace
       :if-nil buffer-read-only
       :transient nil)
      ("x"
       "Start ‘query-replace-regexp’"
       isearch-query-replace-regexp
       :if-nil buffer-read-only
       :transient nil)]]

    [["Toggle"
      ("X"
       "Toggle regexp searching"
       isearch-toggle-regexp
       :transient nil)
      ("S"
       "Toggle symbol searching"
       isearch-toggle-symbol
       :transient nil)
      ("W"
       "Toggle word searching"
       isearch-toggle-word
       :transient nil)
      ("F"
       "Toggle case fold"
       isearch-toggle-case-fold
       :transient nil)
      ("L"
       "Toggle lax whitespace"
       isearch-toggle-lax-whitespace
       :transient nil)]

     ["Misc"
      ("o"
       "occur"
       isearch-occur
       :transient nil)]])
  (keymap-set isearch-mode-map "C-h C-h" #'my/isearch-menu))
#+end_src
*** ediff
#+begin_src elisp
(defvar local-ediff-saved-window-conf nil)
(defun eat/ediff-save-window-conf ()
  (setq local-ediff-saved-window-conf (current-window-configuration)))

(defun eat/ediff-restore-window-conf ()
  (when (window-configuration-p local-ediff-saved-window-conf)
    (set-window-configuration local-ediff-saved-window-conf)))

(setq ediff-window-setup-function #'ediff-setup-windows-plain
      ediff-highlight-all-diffs t
      ediff-split-window-function 'split-window-horizontally
      ediff-merge-split-window-function 'split-window-horizontally)
(with-eval-after-load 'ediff
  ;; Restore window config after quitting ediff
  (add-hook 'ediff-before-setup-hook #'eat/ediff-save-window-conf)
  (add-hook 'ediff-quit-hook #'eat/ediff-restore-window-conf))
#+end_src
*** hl-line
#+begin_src emacs-lisp
(setq hl-line-sticky-flag nil)

;; (when (display-graphic-p)
;;   (add-hook 'prog-mode-hook #'hl-line-mode)
;;   (add-hook 'conf-mode-hook #'hl-line-mode))

(defun eat/hl-line-setup ()
  "Disable `hl-line-mode' if region is active."
  (when (and (bound-and-true-p hl-line-mode)
             (region-active-p))
    (hl-line-unhighlight)))

(with-eval-after-load 'hl-line
  (add-hook 'post-command-hook #'eat/hl-line-setup))
#+end_src

*** pulse
#+begin_src elisp
(custom-set-faces
 '(pulse-highlight-start-face ((t (:inherit region))))
 '(pulse-highlight-face ((t (:inherit region)))))

(defun eat/pulse-momentary-line (&rest _)
  "Pulse the current line."
  (pulse-momentary-highlight-one-line (point)))

(defun eat/pulse-momentary (&rest _)
  "Pulse the region or the current line."
  (if (fboundp 'xref-pulse-momentarily)
      (xref-pulse-momentarily)
    (eat/pulse-momentary-line)))

(defun eat/recenter-and-pulse(&rest _)
  "Recenter and pulse the region or the current line."
  (recenter)
  (eat/pulse-momentary))

(defun eat/recenter-and-pulse-line (&rest _)
  "Recenter and pulse the current line."
  (recenter)
  (eat/pulse-momentary-line))

(dolist (cmd '(recenter-top-bottom
               other-window windmove-do-window-select
               pager-page-down pager-page-up))
  (advice-add cmd :after #'eat/pulse-momentary-line))

(dolist (cmd '(pop-to-mark-command
               pop-global-mark
               compile-goto-error
               goto-last-change))
  (advice-add cmd :after #'eat/recenter-and-pulse))

(add-hook 'imenu-after-jump-hook #'eat/recenter-and-pulse)
(add-hook 'isearch-update-post-hook #'eat/recenter-and-pulse)

(add-hook 'bookmark-after-jump-hook #'eat/recenter-and-pulse-line)
(add-hook 'next-error #'eat/recenter-and-pulse-line)
#+end_src

*** simple
#+begin_src emacs-lisp
(setq visual-line-fringe-indicators '(nil nil)
      ;; List only applicable commands.
      fill-column 72)
#+end_src
*** smerge
#+begin_src elisp
(add-hook 'find-file-hook #'(lambda ()
                              (save-excursion
                                (goto-char (point-min))
                                (when (re-search-forward "^<<<<<<< " nil t)
                                  (smerge-mode 1)))))

(with-eval-after-load 'smerge-mode
  (keymap-set smerge-mode-map "C-c c" #'smerge-keep-current)
  (keymap-set smerge-mode-map "C-c a" #'smerge-smerge-keep-all)
  (keymap-set smerge-mode-map "M-r" #'smerge-refine)
  (keymap-set smerge-mode-map "M-n" #'smerge-next)
  (keymap-set smerge-mode-map "M-p" #'smerge-prev))
#+end_src
*** hideshow
- Seems have error message.
- And to context-menu-mode.
#+begin_src elisp
(add-hook 'prog-mode-hook #'hs-minor-mode)

;; FIXME
(defconst hideshow-folded-face '((t (:inherit 'font-lock-comment-face :box t))))

(defface hideshow-border-face
  '((((background light))
     :background "rosy brown" :extend t)
    (t
     :background "sandy brown" :extend t))
  "Face used for hideshow fringe."
  :group 'hideshow)

(define-fringe-bitmap 'hideshow-folded-fringe
  (vector #b00000000
          #b00000000
          #b00000000
          #b11000011
          #b11100111
          #b01111110
          #b00111100
          #b00011000))

(defun hideshow-folded-overlay-fn (ov)
  "Display a folded region indicator with the number of folded lines."
  (when (eq 'code (overlay-get ov 'hs))
    (let* ((nlines (count-lines (overlay-start ov) (overlay-end ov)))
           (info (format " (%d)..." nlines)))
      ;; fringe indicator
      (overlay-put ov 'before-string (propertize " "
                                                 'display '(left-fringe hideshow-folded-fringe
                                                                        hideshow-border-face)))
      ;; folding indicator
      (overlay-put ov 'display (propertize info 'face hideshow-folded-face)))))

(setq hs-set-up-overlay #'hideshow-folded-overlay-fn)
#+end_src
*** flymake
#+begin_src elisp
(add-hook 'prog-mode-hook #'flymake-mode)
(add-hook 'emacs-lisp-mode-hook #'(lambda ()
                                    (flymake-mode -1)))

(setq-default flymake-diagnostic-functions nil)

(defvar sekiro-flymake-mode-line-format `(:eval (sekiro-flymake-mode-line-format)))
(put 'sekiro-flymake-mode-line-format 'risky-local-variable t)
(defun sekiro-flymake-mode-line-format ()
  (let* ((counter (string-to-number
                   (nth 1
                        (cadr
                         (flymake--mode-line-counter :error t)))))
         (sekiro-flymake (when (> counter 0)
                           'compilation-error)))
    (propertize
     "危"
     'face
     sekiro-flymake)))

(with-eval-after-load 'flymake
  (keymap-set flymake-mode-map "M-p" #'flymake-goto-prev-error)
  (keymap-set flymake-mode-map "M-n" #'flymake-goto-next-error)
  (add-to-list 'mode-line-misc-info
               `(flymake-mode (" [" sekiro-flymake-mode-line-format "] "))))


(add-hook 'flymake-mode-hook
          (lambda ()
            (add-hook 'eldoc-documentation-functions 'flymake-eldoc-function nil t)))
#+end_src
*** ibuffer
#+begin_src emacs-lisp
(fset 'list-buffers 'ibuffer)
(setq-default ibuffer-show-empty-filter-groups nil)
#+end_src
*** eglot
#+begin_src emacs-lisp
(setq eglot-events-buffer-size 0
      eglot-autoshutdown t
      eglot-sync-connect nil       ;; don't block of LSP connection attempts
      eglot-extend-to-xref t       ;; make eglot manage file out of project by `xref-find-definitions'
      eglot-ignored-server-capabilites
      '(:documentHighlightProvider
        :documentFormattingProvider
        :documentRangeFormattingProvider
        :documentLinkProvider
        ;; 好像是会和 treesit 的缩进冲突？不过没有测试 go 的，是群友提
        ;; 出的 cpp 的，不知道 go-ts-mode 的缩进问题和这个有没有关系
        ;; TODO 这个没有生效？在 go 中 if 后面 RET 还是格式化成了 8 空
        ;; 格
        :documentOnTypeFormattingProvider))

(setq-default eglot-workspace-configuration
              '((:gopls
                 (:ui.completion.usePlaceholders . t)
                 (:ui.diagnostic.staticcheck . t)
                 ;; for I have to edit wire.go even ignore it in build time
                 (:build.buildFlags . ["-tags" "wireinject"]))))

(with-eval-after-load 'eglot
  (keymap-set eglot-mode-map "M-RET" #'eglot-code-actions)
  (keymap-set eglot-mode-map "C-c r" #'eglot-rename)
  (keymap-set eglot-mode-map "M-'"   #'eglot-find-implementation)

  (add-to-list 'eglot-server-programs '(rust-mode "rust-analyzer"))
  (add-to-list 'eglot-server-programs '(sql-mode . ("sqls" "-config" "~/.config/sqls/config.yaml")))
  (add-to-list 'eglot-server-programs '(nix-mode . ("nixd")))
  (add-to-list 'eglot-server-programs '(typst-ts-mode . ("typst-lsp")))
  (add-to-list 'eglot-server-programs '(org-mode . ("ltex-ls")))
  (add-to-list 'eglot-server-programs '(markdown-mode . ("ltex-ls")))
  (add-to-list 'eglot-server-programs '(message-mode . ("ltex-ls"))))
#+end_src
*** treesit
1. Install =tree-sitter= on system.
2. Build emacs with tree sitter =--with-tree-sitter=.
3. Install language definitions to =/usr/local/lib= or =~/.config/emacs/tree-sitter= or =treesit-extra-load-path=.
4. On windows, copy dll files to Emacs’s bin directory.

Also check:
[[https://git.savannah.gnu.org/cgit/emacs.git/tree/admin/notes/tree-sitter/starter-guide?h=emacs-29][Emacs tree sitter start guide.]]
[[https://github.com/casouri/tree-sitter-module][Tree sitter language definitions build script.]]
~treesit-language-source-alist~
~treesit-install-language-grammar~

#+begin_src emacs-lisp
(setq
 ;; 没有在 tree-sitter 组织下的 parser
 treesit-language-source-alist
 '((gomod . ("https://github.com/camdencheek/tree-sitter-gomod.git"))
   (yaml . ("https://github.com/ikatyang/tree-sitter-yaml")))
 go-ts-mode-indent-offset 4)

(with-eval-after-load 'go-ts-mode
  (require 'go-mode)

  (setq go-ts-mode-hook go-mode-hook)
  (set-keymap-parent go-ts-mode-map go-mode-map))

(when (treesit-available-p)
  (push '(python-mode . python-ts-mode) major-mode-remap-alist)
  (push '(go-mode . go-ts-mode) major-mode-remap-alist)

  (add-to-list 'auto-mode-alist '("\\.rs\\'" . rust-ts-mode))
  (add-to-list 'auto-mode-alist '("\\.yaml\\'" . yaml-ts-mode))
  (add-to-list 'auto-mode-alist '("\\.toml\\'" . toml-ts-mode)))
#+end_src
*** newsticker
#+begin_src emacs-lisp
(keymap-global-set "C-x W" #'newsticker-show-news)

(setq newsticker-url-list
      '(("Planet Emacslife" "https://planet.emacslife.com/atom.xml")
        ("Mastering Emacs" "http://www.masteringemacs.org/feed/")
        ("Oremacs" "https://oremacs.com/atom.xml")
        ("EmacsCast" "https://pinecast.com/feed/emacscast")
        ("Emacs TIL" "https://emacstil.com/feed.xml")
        ("Emacs Reddit" "https://www.reddit.com/r/emacs.rss")))
#+end_src
*** ispell
#+begin_src emacs-lisp
(setq ispell-program-name "aspell"
      ispell-extra-args '( "-W" "3" "--sug-mode=ultra" "--lang=en_US"
                           ;; run-together allows compound words
                           ;; like "viewport".
                           "--run-together"))
#+end_src
*** message

#+begin_src emacs-lisp
(setq message-kill-buffer-on-exit t
      message-mail-alias-type 'ecomplete
      user-full-name "Liu Bo"
      user-mail-address "liubolovelife@gmail.com"
      message-signature user-full-name
      smtpmail-smtp-user user-mail-address
      send-mail-function #'smtpmail-send-it
      message-send-mail-function #'message-use-send-mail-function
      smtpmail-smtp-server "smtp.gmail.com"
      smtpmail-smtp-service 587)
(defun eat/message-mode-setup ()
  (auto-fill-mode)
  (when (executable-find "ltex-ls")
    (eglot-ensure)))
(add-hook 'message-mode-hook #'eat/message-mode-setup)
#+end_src

*** gnus
#+begin_src elisp
;;; gnus
(setq
 gnus-use-cache t
 gnus-use-scoring nil
 gnus-suppress-duplicates t
 gnus-novice-user nil
 gnus-expert-user t
 gnus-interactive-exit 'quiet
 gnus-inhibit-startup-message t
 gnus-select-method '(nnnil "")
 gnus-secondary-select-methods
 `((nntp "gmane" (nntp-address "news.gmane.io"))
   (nntp "nntp.lore.kernel.org")
   (nnimap "Gmail"
           (nnimap-user "liubolovelife@gmail.com")
           (nnimap-inbox "INBOX")
           (nnimap-address "imap.gmail.com")
           (nnimap-stream ssl)
           (nnimap-expunge 'never)
           ;; @see http://www.gnu.org/software/emacs/manual/html_node/gnus/Expiring-Mail.html
           ;; press 'E' to expire email
           (nnmail-expiry-target "nnimap+Gmail:[Gmail]/Trash")
           (nnmail-expiry-wait 90)))
 ;; Startup functions
 gnus-save-killed-list nil
 gnus-check-new-newsgroups nil
 ;; No other newsreader is used.
 gnus-save-newsrc-file nil
 gnus-read-newsrc-file nil
 gnus-subscribe-newsgroup-method 'gnus-subscribe-interactively
 ;; Emacs 28 introduces a unified query lang
 gnus-search-use-parsed-queries t
 ;; Article mode for Gnus
 gnus-visible-headers (rx line-start (or "From"
                                         "Subject"
                                         "Mail-Followup-To"
                                         "Date"
                                         "To"
                                         "Cc"
                                         "Newsgroups"
                                         "User-Agent"
                                         "X-Mailer"
                                         "X-Newsreader")
                          ":")
 gnus-article-sort-functions '((not gnus-article-sort-by-number)
                               (not gnus-article-sort-by-date))
 gnus-article-browse-delete-temp t
 ;; Display more MINE stuff
 gnus-mime-display-multipart-related-as-mixed t
 ;; Asynchronous support for Gnus
 gnus-asynchronous t
 gnus-use-header-prefetch t
 ;; Cache interface for Gnus
 gnus-cache-enter-articles '(ticked dormant unread)
 gnus-cache-remove-articles '(read)
 gnus-cacheable-groups "^\\(nntp\\|nnimap\\)")

;; Group
(setq
 ;;          indentation ------------.
 ;;  #      process mark ----------. |
 ;;                level --------. | |
 ;;           subscribed ------. | | |
 ;;  %          new mail ----. | | | |
 ;;  *   marked articles --. | | | | |
 ;;                        | | | | | |  Ticked    New     Unread  open-status Group
 gnus-group-line-format "%M%m%S%L%p%P %1(%7i%) %3(%7U%) %3(%7y%) %4(%B%-45G%) %d\n"
 gnus-group-sort-function '(gnus-group-sort-by-level gnus-group-sort-by-alphabet))

(add-hook 'gnus-group-mode-hook #'gnus-topic-mode)

;; Summary
(setq
 ;; Pretty marks
 gnus-sum-thread-tree-root            "┌ "
 gnus-sum-thread-tree-false-root      "◌ "
 gnus-sum-thread-tree-single-indent   "◎ "
 gnus-sum-thread-tree-vertical        "│"
 gnus-sum-thread-tree-indent          "  "
 gnus-sum-thread-tree-leaf-with-other "├─►"
 gnus-sum-thread-tree-single-leaf     "╰─►"
 gnus-summary-line-format "%U%R %3d %[%-23,23f%] %B %s\n"
 ;; Threads!  I hate reading un-threaded email -- especially mailing
 ;; lists.  This helps a ton!
 gnus-summary-make-false-root 'adopt
 gnus-simplify-subject-functions '(gnus-simplify-subject-re gnus-simplify-whitespace)
 gnus-summary-thread-gathering-function 'gnus-gather-threads-by-subject
 ;; Filling in threads
 ;; 2 old articles are enough for memory
 gnus-fetch-old-headers 2
 gnus-fetch-old-ephemeral-headers 2
 gnus-build-sparse-threads 'some
 ;; More threading
 gnus-show-threads t
 gnus-thread-indent-level 2
 gnus-thread-hide-subtree nil
 ;; Sorting
 gnus-thread-sort-functions '(gnus-thread-sort-by-most-recent-date)
 gnus-subthread-sort-functions '(gnus-thread-sort-by-date)
 ;; Viewing
 gnus-view-pseudos 'automatic
 gnus-view-pseudos-separately t
 gnus-view-pseudo-asynchronously t
 ;; No auto select
 gnus-auto-select-first nil
 gnus-auto-select-next nil
 gnus-paging-select-next nil)

(add-hook 'gnus-select-group-hook #'gnus-group-set-timestamp)
(add-hook 'gnus-summary-mode-hook #'hl-line-mode)

#+end_src
*** desktop-save
#+begin_src emacs-lisp
(setq history-length 1000)
(add-hook 'after-init-hook #'savehist-mode)

(setq desktop-auto-save-timeout 300
      desktop-load-locked-desktop t
      desktop-restore-eager 20)
(add-hook 'after-init-hook #'desktop-save-mode)

(defun sanityinc/time-subtract-millis (b a)
  (* 1000.0 (float-time (time-subtract b a))))

(defun sanityinc/desktop-time-restore (orig &rest args)
  (let ((start-time (current-time)))
    (prog1
        (apply orig args)
      (message "Desktop restored in %.2fms"
               (sanityinc/time-subtract-millis (current-time)
                                               start-time)))))
(advice-add 'desktop-read :around 'sanityinc/desktop-time-restore)

(defun sanityinc/desktop-time-buffer-create (orig ver filename &rest args)
  (let ((start-time (current-time)))
    (prog1
        (apply orig ver filename args)
      (message "Desktop: %.2fms to restore %s"
               (sanityinc/time-subtract-millis (current-time)
                                               start-time)
               (when filename
                 (abbreviate-file-name filename))))))
(advice-add 'desktop-create-buffer :around 'sanityinc/desktop-time-buffer-create)

(setq desktop-globals-to-save
      '((comint-input-ring        . 50)
        (compile-history          . 30)
        desktop-missing-file-warning
        (dired-regexp-history     . 20)
        (extended-command-history . 30)
        (face-name-history        . 20)
        (grep-find-history        . 30)
        (grep-history             . 30)
        (magit-revision-history   . 50)
        (minibuffer-history       . 50)
        (org-clock-history        . 50)
        (org-refile-history       . 50)
        (org-tags-history         . 50)
        (query-replace-history    . 60)
        (read-expression-history  . 60)
        (regexp-history           . 60)
        (shell-command-history    . 50)
        ;; Default value.
        tags-file-name
        tags-table-list
        (search-ring              . 20)
        (regexp-search-ring       . 20)
        register-alist
        (file-name-history        . 100)))
#+end_src
*** hi-lock
#+begin_quote
I use global-hi-lock-mode in Emacs to introduce color-highlighting
into my notes using special punctuation. For instance, when I use
"??", it renders the line yellow, signaling outstanding
questions. "@@" transforms the line into green, emphasizing noteworthy
ideas or observations. Finally, "!!" marks the line as red, indicating
a warning or an important point. This system significantly enhances my
writing/thinking workflow.
#+end_quote
*** misc
#+begin_src emacs-lisp
;; Quick editing in `describe-variable'
(with-eval-after-load 'help-fns
  (put 'help-fns-edit-variable 'disabled nil))

(add-hook 'on-first-file-hook #'global-goto-address-mode)
(add-hook 'on-first-file-hook #'global-auto-revert-mode)
(add-hook 'on-first-file-hook #'save-place-mode)

(setq outline-minor-mode-cycle t
      outline-minor-mode-highlight t)

(setq-default vc-handled-backends '(Git))

(add-hook 'prog-mode-hook #'whitespace-mode)
(add-hook 'conf-mode-hook #'whitespace-mode)
(setq whitespace-style '(face trailing))

(add-hook 'on-first-buffer-hook #'winner-mode)
(setq winner-dont-bind-my-keys t)

(add-hook 'on-init-ui-hook #'context-menu-mode)

;;; disable blink cursor
(add-hook 'after-init-hook (lambda () (blink-cursor-mode -1)))

;;; line number
(setq display-line-numbers-width 3)

(add-hook 'prog-mode-hook
          #'(lambda ()
              (setq-local comment-auto-fill-only-comments t)
              (turn-on-auto-fill)))

(setq eldoc-idle-delay 1)
(setq eldoc-documentation-function 'eldoc-documentation-compose)

(add-hook 'prog-mode-hook #'subword-mode)

(add-hook 'prog-mode-hook #'electric-pair-local-mode)
(setq electric-pair-inhibit-predicate 'electric-pair-conservative-inhibit)

(setq show-paren-when-point-in-periphery t
      show-paren-context-when-offscreen 'overlay
      show-paren-when-point-inside-paren t
      show-paren-context-when-offscreen t)

(when (executable-find "readable")
  (setq eww-retrieve-command '("readable")))

(add-hook 'eshell-mode-hook (lambda () (setq outline-regexp eshell-prompt-regexp)))
#+end_src
** Next version
#+begin_src emacs-lisp
(when (>= emacs-major-version 30)
  (add-to-list 'help-fns-describe-function-functions #'shortdoc-help-fns-examples-function))
#+end_src
* Libs
** fullframe
#+begin_src emacs-lisp
(eat/pkg fullframe)
#+end_src
** hide-mode-line
#+begin_src emacs-lisp
(eat/pkg hide-mode-line)
#+end_src
** bind
#+begin_src emacs-lisp
(eat/pkg bind)
#+end_src
** elpa-mirror
#+begin_src emacs-lisp
(eat/pkg elpa-mirror)
(setq elpamr-default-output-directory "~/Sync/myelpa")
#+end_src
** visual-fill-column
#+begin_src emacs-lisp
(eat/pkg visual-fill-column)
(add-hook 'visual-fill-column-mode-hook #'visual-line-mode)
(setq visual-fill-column-center-text t)
#+end_src

** Better built in
*** project-x
Recognize any directory with a .project file as a project.
Save and restore project files and window configurations across sessions.

#+begin_src emacs-lisp
(eat/pkg project-x "https://github.com/karthink/project-x")
(with-eval-after-load 'project
  (project-x-mode 1))
#+end_src
*** ibuffer-vc
#+begin_src elisp
;; Modify the default ibuffer-formats (toggle with `)
(setq ibuffer-formats
      '((mark modified read-only vc-status-mini " "
              (name 22 22 :left :elide)
              " "
              (size-h 9 -1 :right)
              " "
              (mode 12 12 :left :elide)
              " "
              vc-relative-file)
        (mark modified read-only vc-status-mini " "
              (name 22 22 :left :elide)
              " "
              (size-h 9 -1 :right)
              " "
              (mode 14 14 :left :elide)
              " "
              (vc-status 12 12 :left)
              " "
              vc-relative-file)))

(with-eval-after-load 'ibuffer
  (setq ibuffer-filter-group-name-face 'font-lock-doc-face)
  ;; Use human readable Size column instead of original one
  (define-ibuffer-column size-h
    (:name "Size" :inline t)
    (file-size-human-readable (buffer-size)))

  (with-eval-after-load 'fullframe
    (fullframe ibuffer ibuffer-quit)))

(eat/pkg ibuffer-vc)

(defun ibuffer-set-up-preferred-filters ()
  (ibuffer-vc-set-filter-groups-by-vc-root)
  (unless (eq ibuffer-sorting-mode 'filename/process)
    (ibuffer-do-sort-by-filename/process)))
;; Ibuffer-vc
(add-hook 'ibuffer-hook #'ibuffer-set-up-preferred-filters)
#+end_src
*** isearch-mb
#+begin_src emacs-lisp
(eat/pkg isearch-mb)
(add-hook 'isearch-mode-hook #'isearch-mb-mode)

(define-advice isearch-mb--update-prompt (:around (fn &rest args) show-case-fold-info)
  "Show case fold info in the prompt."
  (cl-letf* ((isearch--describe-regexp-mode-orig
              (symbol-function 'isearch--describe-regexp-mode))
             ((symbol-function 'isearch--describe-regexp-mode)
              (lambda (regexp-function &optional space-before)
                (concat (if isearch-case-fold-search "[Case Fold] " "")
                        (funcall isearch--describe-regexp-mode-orig
                                 regexp-function space-before)))))
    (apply fn args)))

(with-eval-after-load "isearch-mb"
  (bind isearch-mb-minibuffer-map
        "C-h C-h" #'my/isearch-menu
        "C-c C-o" #'isearch-occur))
#+end_src
*** eldoc-box
#+begin_src emacs-lisp
(eat/pkg eldoc-box)
(setq eldoc-box-only-multi-line t)
(add-hook 'eglot-managed-mode-hook #'eldoc-box-hover-mode)
#+end_src
*** w3m, read html mail
#+begin_src emacs-lisp
(eat/pkg w3m)
(setq mm-text-html-renderer 'w3m)
#+end_src
*** comint-mime
A special command, =mimecat=, is provided to display content. Again,
this works for images, HTML, LaTeX snippets, etc.

#+begin_src emacs-lisp
(eat/pkg comint-mime)

(add-hook 'shell-mode-hook 'comint-mime-setup)
(add-hook 'inferior-python-mode-hook 'comint-mime-setup)

(when (executable-find "ipython3")
  (setq python-shell-interpreter "ipython3"
        python-shell-interpreter-args "--simple-prompt --classic"))
#+end_src
** page-break-lines
#+begin_src emacs-lisp
(eat/pkg page-break-lines)
(add-hook 'after-init-hook 'global-page-break-lines-mode)
#+end_src
* Window
** window-numbering
#+begin_src emacs-lisp
(eat/pkg window-numbering)
(add-hook 'after-init-hook #'window-numbering-mode)
#+end_src
** ace-window
#+begin_src emacs-lisp
(eat/pkg ace-window)

(keymap-global-set "M-o" 'ace-window)
(keymap-global-set "C-x C-o" 'ace-window)
(keymap-substitute global-map 'other-window 'ace-window)

(setq aw-keys '(?a ?o ?e ?u ?i))
#+end_src
** popper
#+begin_src emacs-lisp
(eat/pkg popper)

(add-hook 'on-first-buffer-hook #'popper-mode)

(setq popper-reference-buffers
      '("\\*Messages\\*"
        "Output\\*$"
        "\\*Async Shell Command\\*"
        "\\*Compile-Log\\*"
        "\\*Completions\\*"
        "\\*Warnings\\*"

        "^\\*eshell.*\\*$" eshell-mode  ;eshell as a popup
        "^\\*shell.*\\*$"  shell-mode   ;shell as a popup
        "^\\*term.*\\*$"   term-mode    ;term as a popup

        ;; help & message
        help-mode
        compilation-mode

        ghelp-page-mode
        "^\\*eat.*\\*$" eat-mode))

(with-eval-after-load 'project
  (setq popper-group-function 'popper-group-by-project))

(with-eval-after-load 'popper
  (keymap-global-set "C-M-`" #'popper-toggle-type)
  (defun my-popper-fit-window-height (win)
    "Determine the height of popup window WIN by fitting it to the buffer's content."
    (fit-window-to-buffer
     win
     (floor (frame-height) 3)
     (floor (frame-height) 3)))
  (setq popper-window-height #'my-popper-fit-window-height))
#+end_src
* UI
** nerd-icons
#+begin_src emacs-lisp
;; run `nerd-icons-install-fonts'
(eat/pkg nerd-icons)

;; ttf-nerd-fonts-symbols-1000-em-mono
(eat/pkg nerd-icons-ibuffer)
(add-hook 'ibuffer-mode-hook #'nerd-icons-ibuffer-mode)

(eat/pkg nerd-icons-dired)
(add-hook 'dired-mode-hook
          #'(lambda ()
              (if dirvish-override-dired-mode
                  (nerd-icons-dired-mode -1)
                (nerd-icons-dired-mode 1))))

(eat/pkg nerd-icons-completion)
(add-hook 'minibuffer-setup-hook #'nerd-icons-completion-mode)
#+end_src
** breadcrumb
#+begin_src emacs-lisp
(eat/pkg breadcrumb)
(setq-default frame-title-format
              '((:eval (breadcrumb-project-crumbs))
                (:eval (and imenu--index-alist
                            (concat "  ◊  " (breadcrumb-imenu-crumbs))))))
#+end_src
** default-text-scale
#+begin_src emacs-lisp
(eat/pkg default-text-scale)
(bind global-map
      "C-x C-=" #'default-text-scale-increase
      "C-x C--" #'default-text-scale-decrease)
#+end_src
** hl-todo
#+begin_src emacs-lisp
(eat/pkg hl-todo)
(add-hook 'dired-mode-hook #'hl-todo-mode)
(add-hook 'prog-mode-hook #'hl-todo-mode)
(add-hook 'conf-mode-hook #'hl-todo-mode)
#+end_src
** minions
#+begin_src emacs-lisp
(eat/pkg minions)
(add-hook 'after-init-hook 'minions-mode)
#+end_src
** fontifi-patch, fontifying patch diffs in mail
#+begin_src emacs-lisp
(eat/pkg fontify-patch "https://github.com/whame/fontify-patch")

(add-hook 'gnus-part-display-hook 'fontify-patch-buffer)
#+end_src

** themes
#+begin_src emacs-lisp
(eat/pkg almost-mono-themes)
(eat/pkg standard-themes)
(eat/pkg kaolin-themes)
(eat/pkg spacemacs-theme)

(eat/pkg nano-theme "https://github.com/mrunhap/nano-theme.el")
(eat/pkg carbon-theme "https://github.com/DogLooksGood/carbon-theme")
(eat/pkg paperlike-theme "https://github.com/DogLooksGood/paperlike-theme")
(add-to-list 'custom-theme-load-path (expand-file-name "elpa/nano-theme" user-emacs-directory))
(add-to-list 'custom-theme-load-path (expand-file-name "elpa/carbon-theme" user-emacs-directory))
(add-to-list 'custom-theme-load-path (expand-file-name "elpa/paperlike-theme" user-emacs-directory))
#+end_src

** auto-dark
#+begin_src emacs-lisp
(eat/pkg auto-dark)
(autoload #'auto-dark-mode "auto-dark")
(add-hook 'after-make-window-system-frame-hooks #'auto-dark-mode)

(setq auto-dark-dark-theme 'standard-light
      auto-dark-light-theme 'standard-dark)
#+end_src
* Edit
** expand-region
#+begin_src emacs-lisp
(eat/pkg expreg)
(keymap-global-set "C-=" #'expreg-expand)
(keymap-global-set "C--" #'expreg-contract)
#+end_src
** meow
*** Setup
#+begin_src elisp
(defun meow-setup-dvorak ()
  (interactive)
  (setq meow-cheatsheet-layout meow-cheatsheet-layout-dvorak)

  (meow-motion-overwrite-define-key
   '("<escape>" . mode-line-other-buffer)
   '("'" . repeat)
   '(")" . tab-bar-switch-to-prev-tab)
   '("}" . tab-bar-switch-to-next-tab))

  (meow-leader-define-key
   '("b" . switch-to-buffer)
   '("k" . kill-this-buffer)
   (cons "p" project-prefix-map)

   ;; Use SPC (0-9) for digit arguments.
   '("1" . meow-digit-argument)
   '("2" . meow-digit-argument)
   '("3" . meow-digit-argument)
   '("4" . meow-digit-argument)
   '("5" . meow-digit-argument)
   '("6" . meow-digit-argument)
   '("7" . meow-digit-argument)
   '("8" . meow-digit-argument)
   '("9" . meow-digit-argument)
   '("0" . meow-digit-argument))

  (meow-normal-define-key
   '("S" . save-buffer)
   '("Q" . flymake-goto-prev-error)
   '("J" . flymake-goto-next-error)
   '(")" . tab-bar-switch-to-prev-tab)
   '("}" . tab-bar-switch-to-next-tab)
   '("-" . avy-goto-char-timer)

   ;;; Below config basicly won't change anymore.
   '("'" . repeat)
   '("!" . dirvish-side)
   '(":" . gptel-menu)
   '("~" . eat-project)
   '("`" . eat)
   '("_" . xeft)
   '("@" . hs-toggle-hiding)
   '("q" . eat/quit)
   '("/" . comment-dwim)
   '("=" . expreg-expand)
   '("+" . expreg-contract)
   '("v" . scroll-up-command)
   '("V" . scroll-down-command)
   '("\"" . insert-pair)
   '("[" . insert-pair)
   '("{" . insert-pair)
   '("(" . insert-pair)
   '("]" . delete-pair)

   '("?" . meow-cheatsheet)
   '("<escape>" . mode-line-other-buffer)
   '(";" . meow-reverse)

   ;; mark/find/till/visit, most used in beacon mode
   '("O" . meow-mark-word)
   '("E" . meow-mark-symbol)
   '("M" . meow-search)
   '("f" . meow-find)
   '("F" . meow-till)
   '("l" . meow-visit)

   ;; kill/delete/change/replace
   '("d" . meow-delete)
   '("D" . meow-backward-delete)
   '("k" . meow-kill)
   '("K" . meow-kill-whole-line)
   '("C" . meow-change)
   '("r" . meow-replace)

   ;; yank/pop
   '("s" . meow-save)
   '("y" . meow-yank)

   ;; grab
   '("G" . meow-grab)
   '("X" . meow-sync-grab)
   '("R" . meow-swap-grab)
   '("z" . meow-pop-selection)

   ;; undo
   '("u" . meow-undo)
   '("U" . meow-undo-in-selection)

   ;; expand by numbers
   '("0" . meow-expand-0)
   '("9" . meow-expand-9)
   '("8" . meow-expand-8)
   '("7" . meow-expand-7)
   '("6" . meow-expand-6)
   '("5" . meow-expand-5)
   '("4" . meow-expand-4)
   '("3" . meow-expand-3)
   '("2" . meow-expand-2)
   '("1" . meow-expand-1)

   ;; line operation
   '("j" . meow-join)
   '("e" . meow-line)
   '("o" . meow-block)

   ;; query replace & kmacro
   '("#" . meow-start-kmacro-or-insert-counter)
   '("$" . meow-end-or-call-kmacro)
   '("&" . meow-query-replace)
   '("%" . meow-query-replace-regexp)

   ;; start keypad
   '("m" . meow-keypad-start)
   '("g" . meow-keypad-start)
   '("c" . meow-keypad-start)
   '("x" . meow-keypad-start)

   ;; thing
   '("," . meow-inner-of-thing)
   '("." . meow-bounds-of-thing)
   '("<" . meow-beginning-of-thing)
   '(">" . meow-end-of-thing)

   ;; movement, like hjkl
   '("h" . meow-left)
   '("H" . meow-left-expand)
   '("t" . meow-right)
   '("T" . meow-right-expand)
   '("n" . meow-next)
   '("N" . meow-next-expand)
   '("p" . meow-prev)
   '("P" . meow-prev-expand)

   ;; insert above/below
   '("i" . meow-insert)
   '("I" . meow-open-above)
   '("A" . meow-open-below)
   '("a" . meow-append)

   ;; move/mark by word/symbol
   '("b" . meow-back-word)
   '("B" . meow-back-symbol)
   '("W" . meow-next-symbol)
   '("w" . meow-next-word)))
#+end_src
*** Config
#+begin_src elisp
(eat/pkg meow)
(require 'meow)

(setq meow-esc-delay 0.001
      meow-keypad-leader-dispatch "C-c"
      meow-keypad-start-keys nil
      meow-replace-state-name-list
      '((normal . "N")
        (beacon . "B")
        (insert . "I")
        (motion . "M")
        (keypad . "K")))

(meow-thing-register 'angle '(pair ("<") (">")) '(pair ("<") (">")))
(add-to-list 'meow-char-thing-table '(?a . angle))
(meow-thing-register 'backquote '(regexp "`" "`") '(regexp "`" "`"))
(add-to-list 'meow-char-thing-table '(?` . backquote))

(meow-setup-dvorak)
(meow-setup-indicator)
(meow-global-mode 1)
#+end_src
*** Disable flymake in insert mode
#+begin_src emacs-lisp
(add-hook 'meow-insert-exit-hook (lambda () (setq flymake-no-changes-timeout 0.5)))
(add-hook 'meow-insert-enter-hook (lambda () (setq flymake-no-changes-timeout nil)))
#+end_src
** separedit
#+begin_src emacs-lisp
(eat/pkg separedit)
(keymap-global-set "C-c '" #'separedit)
#+end_src

** vundo
#+begin_src emacs-lisp
(eat/pkg vundo)
(eat/pkg undo-hl "https://github.com/casouri/undo-hl.git")
(add-hook 'prog-mode-hook #'undo-hl-mode)
(add-hook 'conf-mode-hook #'undo-hl-mode)
(keymap-global-set "C-z" #'vundo)
#+end_src
** rime
If install emacs with nix:
Set =rime-emacs-module-header-root= to =emacs/include=.
set  to =librime=.

#+begin_src elisp
;; curl -L -O https://github.com/rime/librime/releases/download/1.7.2/rime-1.7.2-osx.zip
;; unzip rime-1.7.2-osx.zip -d ~/.config/emacs/librime
;; rm -rf rime-1.7.2-osx.zip
(eat/pkg rime)

(if eat/macp
    (setq rime-librime-root (expand-file-name "librime/dist" user-emacs-directory))
  (setq  rime-share-data-dir "~/.local/share/fcitx5/rime"))
(setq
 rime-disable-predicates '(meow-normal-mode-p
                           ;; meow-motion-mode-p
                           meow-keypad-mode-p
                           meow-beacon-mode-p)
 rime-inline-predicates '(rime-predicate-space-after-cc-p
                          rime-predicate-current-uppercase-letter-p)
 rime-translate-keybindings '("C-f" "C-b" "C-n" "C-p" "C-g" "C-v" "M-v")
 rime-inline-ascii-holder ?a
 default-input-method "rime"
 rime-cursor "|"
 rime-show-candidate 'minibuffer)
(with-eval-after-load 'rime
  (define-key rime-active-mode-map [tab] 'rime-inline-ascii)
  (keymap-set rime-mode-map "M-j" 'rime-force-enable))
#+end_src
*** rime-regexp
Only work for orderless for now.

#+begin_src elisp
(eat/pkg rime-regexp "https://github.com/colawithsauce/rime-regexp.el")
(with-eval-after-load "rime"
  (rime-regexp-mode))
#+end_src

** jit-spell
jit-spell relies on the =ispell= library to pick a spell checking
program and dictionaries.

#+begin_src emacs-lisp
(eat/pkg jit-spell)

(when (executable-find "aspell")
  (add-hook 'text-mode-hook 'jit-spell-mode)
  (add-hook 'prog-mode-hook 'jit-spell-mode))
#+end_src
* Completion
** vertico
#+begin_src emacs-lisp
(eat/pkg vertico)

(add-hook 'after-init-hook #'vertico-mode)
(add-hook 'rfn-eshadow-update-overlay-hook #'vertico-directory-tidy)

(setq vertico-resize nil
      vertico-count 17)

(with-eval-after-load 'vertico
  (bind vertico-map
        "DEL" #'vertico-directory-delete-char
        "RET" #'vertico-directory-enter
        "M-DEL" #'vertico-directory-delete-word))
#+end_src
** marginalia
#+begin_src emacs-lisp
(eat/pkg marginalia)
(add-hook 'after-init-hook #'marginalia-mode)
#+end_src
** orderless
#+begin_src emacs-lisp
(eat/pkg orderless)

(defun sanityinc/use-orderless-in-minibuffer ()
  (setq-local completion-styles '(substring orderless)))
(add-hook 'minibuffer-setup-hook #'sanityinc/use-orderless-in-minibuffer)
#+end_src
** embark
#+begin_src emacs-lisp
(eat/pkg embark)
(eat/pkg embark-consult)

(bind (global-map
       "C-." #'embark-act)
      (minibuffer-local-map
       "C-c C-o" #'embark-export))

(with-eval-after-load 'embark
  ;; Hide the mode line of the Embark live/completions buffers
  (add-to-list 'display-buffer-alist
               '("\\`\\*Embark Collect \\(Live\\|Completions\\)\\*"
                 nil
                 (window-parameters (mode-line-format . none))))

  (with-eval-after-load 'consult
    (add-hook 'embark-collect-mode-hook #'consult-preview-at-point-mode)))
#+end_src

** consult
#+begin_src emacs-lisp
(eat/pkg consult)
(eat/pkg consult-dir)
(eat/pkg consult-eglot)

(setq consult-narrow-key             "<"
      consult-preview-key            "M-."
      xref-show-xrefs-function       #'consult-xref
      xref-show-definitions-function #'consult-xref)

;; Use `consult-completion-in-region' if Vertico is enabled.
;; Otherwise use the default `completion--in-region' function.
(setq completion-in-region-function
      (lambda (&rest args)
        (apply (if vertico-mode
                   #'consult-completion-in-region
                 #'completion--in-region)
               args)))

(bind global-map
      "M-l"            #'consult-line
      [remap yank-pop] #'consult-yank-pop
      ;; C-x bindings
      [remap switch-to-buffer              ] #'consult-buffer
      [remap switch-to-buffer-other-window ] #'consult-buffer-other-window
      [remap switch-to-buffer-other-tab    ] #'consult-buffer-other-tab
      [remap switch-to-buffer-other-frame  ] #'consult-buffer-other-frame
      [remap project-switch-to-buffer      ] #'consult-project-buffer
      [remap project-find-regexp           ] (cond (my/executable-find-rg #'consult-ripgrep)
                                                   (t #'consult-git-grep))
      [remap bookmark-jump                 ] #'consult-bookmark
      [remap recentf-open-files            ] #'consult-recent-file
      ;; M-g for go to things
      "M-g e"           #'consult-compile-error
      "M-g f"           #'consult-flymake
      "M-g o"           #'consult-outline
      "M-g m"           #'consult-mark
      [remap imenu]     #'consult-imenu
      [remap goto-line] #'consult-goto-line
      ;; register
      "M-#"   #'consult-register-load
      "C-M-'" #'consult-register-store
      "C-M-#" #'consult-register)

;;; consult-dir
(keymap-substitute global-map #'list-directory #'consult-dir)
(with-eval-after-load 'vertico
  (bind vertico-map
        "C-x C-d" #'consult-dir
        "C-x C-j" #'consult-dir-jump-file))
#+end_src

** company

- use C-p/C-n to select prev/next
- use tab to do complete
- free ret, popup will no longer interrupt typing
- when disable company, use =completion-at-point= to do it manually

*** enable
#+begin_src emacs-lisp
(eat/pkg company)

(add-hook 'after-init-hook 'global-company-mode)
#+end_src
*** basic config
#+begin_src emacs-lisp
(defun my-company-capf--candidates (func &rest args)
  "Try default completion styles."
  (let ((completion-styles '(basic partial-completion)))
    (apply func args)))
(advice-add 'company-capf--candidates :around 'my-company-capf--candidates)

(setq company-minimum-prefix-length 2
      company-idle-delay 0.1 ;; time for snippet expand
      company-require-match nil)
#+end_src
*** frontend
#+begin_src emacs-lisp
;; Frontends
;; don't need preview frontends with copilot
;; use C-h to show doc(maybe use eldoc-box) instead echo- frontend
;; so only config tooltip frontend
(setq company-frontends '(company-pseudo-tooltip-frontend)
      company-tooltip-align-annotations t
      ;; no matter if a tooltip is shown above or below point, the
      ;; candidates are always listed starting near point.
      company-tooltip-width-grow-only t
      ;; icon in front of a candidate, make it looks like Atom
      company-format-margin-function #'company-text-icons-margin
      company-text-icons-add-background t)
#+end_src
*** backend
#+begin_src emacs-lisp
;; Backends
;; Do not use company-capf with company-yasnippet, it will cause
;; completions list messed up.
(setq company-backends '(company-capf
                         company-files
                         (;;searching for completion candidates inside the contents of the open buffer(s)
                          company-dabbrev-code
                          ;; provides completions from programming language keywords
                          company-keywords)
                         company-dabbrev)
      company-files-exclusions '(".git/" ".DS_Store")
      ;; search from the buffers with the same majar mode
      company-dabbrev-other-buffers t
      company-dabbrev-ignore-case nil
      company-dabbrev-downcase nil
      company-dabbrev-code-ignore-case nil
      company-dabbrev-code-everywhere t)
#+end_src
*** keybinding
#+begin_src emacs-lisp
(with-eval-after-load "company"
  ;; use tab to do complete and free ret
  (bind company-active-map
        [tab] #'company-complete-selection
        "TAB" #'company-complete-selection
        "C-s" #'company-filter-candidates
        ;; Free SPC and RET, popup will no longer interrupt typing.
        [escape] nil
        [return] nil
        "RET" nil
        "SPC" nil))
#+end_src
*** do complete manually
#+begin_src emacs-lisp
(setq company-idle-delay nil)

(with-eval-after-load 'company
  (keymap-substitute company-mode-map #'completion-at-point #'company-complete)
  (keymap-set company-mode-map "C-M-i" #'company-complete))
#+end_src
** yasnippet
#+begin_src elisp
(eat/pkg yasnippet)

(add-hook 'prog-mode-hook #'yas-minor-mode)
(add-hook 'conf-mode-hook #'yas-minor-mode)

(with-eval-after-load 'yasnippet
  (let ((inhibit-message t))
    (yas-reload-all)))
#+end_src
** copilot

#+begin_src emacs-lisp
(eat/pkg copilot "https://github.com/zerolfx/copilot.el")

;; 由于 `lisp-indent-offset' 的默认值是 nil，在编辑 elisp 时每敲一个字
;; 符都会跳出一个 warning，将其默认值设置为 t 以永不显示这个 warning
(setq-default copilot--indent-warning-printed-p t)

(add-hook 'prog-mode-hook 'copilot-mode)

(with-eval-after-load 'copilot

  ;; 文件超出 `copilot-max-char' 的时候不要弹出一个 warning 的 window
  (defun my-copilot-get-source-suppress-warning (original-function &rest args)
    "Advice to suppress display-warning in copilot--get-source."
    (cl-letf (((symbol-function 'display-warning) (lambda (&rest args) nil)))
      (apply original-function args)))
  (advice-add 'copilot--get-source :around #'my-copilot-get-source-suppress-warning)

  (add-to-list 'copilot-major-mode-alist '("go" . "go"))
  (add-to-list 'copilot-major-mode-alist '("go-ts" . "go"))
  (bind copilot-completion-map
        "C-g" #'copilot-clear-overlay
        "C-f" #'copilot-accept-completion
        "C-e" #'copilot-accept-completion-by-line
        "M-f" #'copilot-accept-completion-by-word
        "M-p" #'copilot-previous-completion
        "M-n" #'copilot-next-completion)
  ;; only enable copilot in meow insert mode
  (with-eval-after-load 'meow
    (add-to-list 'copilot-enable-predicates 'meow-insert-mode-p)))
#+end_src
* Tools
** translate
#+begin_src elisp
(eat/pkg fanyi)
(eat/pkg immersive-translate)

;; Write to ~/.authinfo
;; machine deepl.com login apikey password ${key}
(setq immersive-translate-backend 'deepl)

(defun eat/translate ()
  (interactive)
  (if (use-region-p)
      (immersive-translate-paragraph)
    (fanyi-dwim2)))
(keymap-global-set "C-c y" #'eat/translate)

(defun eat/context-translate (menu click)
  "My context MENU to translate text."
  (define-key-after menu [dictionary-lookup]
    '(menu-item "Translate" eat/translate))
  menu)
;; hook into context menu
(add-hook 'context-menu-functions #'eat/context-translate)
#+end_src
** ghelp
#+begin_src elisp
(eat/pkg ghelp "https://github.com/casouri/ghelp.git")

(autoload #'ghelp-describe          "ghelp")
(autoload #'ghelp-describe-function "ghelp")
(autoload #'ghelp-describe-variable "ghelp")
(autoload #'ghelp-describe-key      "ghelp")
(autoload #'ghelp-describe-elisp    "ghelp")

(bind global-map
      "C-h C-h" #'ghelp-describe
      "C-h f"   #'ghelp-describe-function
      "C-h v"   #'ghelp-describe-variable
      "C-h k"   #'ghelp-describe-key
      "C-h o"   #'ghelp-describe-elisp)
(with-eval-after-load 'ghelp
  (bind global-map "C-h r" #'ghelp-resume))
#+end_src

** d2
#+begin_src emacs-lisp
(eat/pkg d2-mode)
(eat/pkg ob-d2)

(add-to-list 'auto-mode-alist '("\\.d2" . d2-mode))
#+end_src
** pastebin
#+begin_src emacs-lisp
(eat/pkg webpaste)

(setq webpaste-paste-confirmation t
      webpaste-add-to-killring t
      webpaste-provider-priority '("paste.mozilla.org")
      webpaste-open-in-browser nil)
#+end_src
** AI
*** aichat, use bind ai

- Install the cookie editor extension for [[https://microsoftedge.microsoft.com/addons/detail/cookieeditor/neaplmfkghagebokkhpjpoebhdledlfi][Egde]]
- Go to bing.com
- Open the extension
- Click “Export” on the bottom right (This saves your cookies to clipboard)
- Paste your cookies into a file cookies.json
- Set =aichat-bingai-cookies-file= to your cookies.json path

For openai, set api key in auth file:
=machine platform.openai.com login aichat-openai password your-app-key=

#+begin_src elisp
(eat/pkg websocket)
(eat/pkg async-await)
(eat/pkg markdown-mode)
(eat/pkg emacs-aichat "https://github.com/xhcoding/emacs-aichat")
(setq aichat-bingai-cookies-file "~/Dropbox/.bingcookies.json"
      aichat-bingai-chat-file "~/Sync/aichat.md")
(autoload #'aichat-bingai-chat "aichat-bingai.el" nil t)
(autoload #'aichat-bingai-assistant "aichat-bingai.el" nil t)
(autoload #'aichat-openai-assistant "aichat-openai.el" nil t)
#+end_src
*** gptel

Custome =gptel-backend= as you need, here is an example:
#+begin_example
(setq-default gptel-backend
                (gptel-make-openai
                 "ChatGPT"
                 :header (lambda () `(("Authorization" . ,(concat "Bearer " (gptel--get-api-key)))))
                 :key 'gptel-api-key
                 :host "api.openai-sb.com"
                 :stream t
                 :models '("gpt-3.5-turbo-16k")))
#+end_example


#+begin_src emacs-lisp
(eat/pkg gptel)

;; store gpt key in ~/.authinfo
;; machine api.openai.com login apikey password TOKEN
(setq gptel-default-mode 'org-mode)

(add-hook 'gptel-mode-hook #'visual-fill-column-mode)
#+end_src
** atomic-chrome, edit browser text with emacs
#+begin_src emacs-lisp
;; https://ghosttext.fregante.com/welcome/
(eat/pkg atomic-chrome)
(setq atomic-chrome-buffer-open-style 'frame)
(add-hook 'after-init-hook #'atomic-chrome-start-server)
#+end_src
** nov, epub reader
#+begin_src emacs-lisp
(eat/pkg nov)
#+end_src
** pdf-tools, pdf reader
#+begin_src emacs-lisp
(eat/pkg pdf-tools)

(autoload #'pdf-view-mode "pdf-tools")
(add-to-list 'auto-mode-alist '("\\.pdf\\'" . pdf-view-mode))

(setq-default pdf-view-display-size 'fit-page)
;; Enable hiDPI support, but at the cost of memory! See politza/pdf-tools#51
(setq pdf-view-use-scaling t
      pdf-view-use-imagemagick nil)

(with-eval-after-load "pdf-tools"
  (pdf-tools-install-noverify)
  (bind pdf-view-mode-map
        [remap scroll-up-command] #'pdf-view-scroll-up-or-next-page
        [remap scroll-down-command] #'pdf-view-scroll-down-or-previous-page))
#+end_src
** xeft, search notes

Need =xapian-core=.

#+begin_src elisp
(eat/pkg xeft)

(setq xeft-directory "~/Dropbox/org/roam"
      xeft-database "~/.xeft/db"
      xeft-default-extension  "org")

(defun xeft-setup ()
  (bklink-minor-mode 1)
  (auto-fill-mode 1))

(with-eval-after-load 'xeft
  (add-hook 'xeft-find-file-hook #'xeft-setup)
  (require 'flique))
#+end_src

** wgrep
#+begin_src emacs-lisp
(eat/pkg wgrep)
(setq wgrep-change-readonly-file t)
(add-hook #'grep-setup-hook #'wgrep-setup)
#+end_src

** urgrep
#+begin_src elisp
(eat/pkg urgrep)

(with-eval-after-load 'urgrep
  (require 'urgrep-wgrep))
#+end_src
** avy
#+begin_src elisp
(eat/pkg avy)
(with-eval-after-load 'avy
  (setq avy-background t
        avy-style 'pre))
#+end_src

* Programming
** Tools
*** direnv
#+begin_src emacs-lisp
(eat/pkg envrc)
(add-hook 'after-init-hook #'envrc-global-mode)
#+end_src
*** restclient
#+begin_src emacs-lisp
(eat/pkg restclient)

(add-to-list 'auto-mode-alist '("\\.rest\\'" . restclient-mode))

(defun restclient-buffer ()
  "Work with `rest' in the *restclient* buffer."
  (interactive)
  (with-current-buffer (get-buffer-create "*restclient*")
    (restclient-mode)
    (pop-to-buffer (current-buffer))))
#+end_src

*** paredit
#+begin_src emacs-lisp
(eat/pkg paredit)

(add-hook 'emacs-lisp-mode-hook #'paredit-mode)
(add-hook 'lisp-interaction-mode-hook #'paredit-mode)
(add-hook 'scheme-mode-hook #'paredit-mode)
(add-hook 'lisp-mode-hook #'paredit-mode)
#+end_src
*** puni
#+begin_src emacs-lisp
(eat/pkg puni)
;; (:with-hook (emacs-lisp-mode-hook scheme-mode-hook clojure-mode-hook)
;;   (:hook puni-mode))
;; (:bind
;;  "M-r" 'puni-splice
;;  "C-(" 'puni-slurp-backward
;;  "C-)" 'puni-slurp-forward
;;  "C-{" 'puni-barf-backward
;;  "C-}" 'puni-barf-forward)
#+end_src
*** apheleia, run formatter
#+begin_src elisp
(eat/pkg apheleia)

(setq apheleia-remote-algorithm 'local)

(add-hook 'go-mode-hook #'apheleia-mode)

(with-eval-after-load 'apheleia
  (when (executable-find "goimports")
    (push '(go-ts-mode . gofmt) apheleia-mode-alist)
    (setf (alist-get 'gofmt apheleia-formatters)
          '("goimports"))))
#+end_src
*** indent-bars
#+begin_src emacs-lisp
(eat/pkg indent-bars "https://github.com/jdtsmith/indent-bars.git")

(add-hook 'python-base-mode-hook #'indent-bars-mode)
(add-hook 'yaml-mode-hook #'indent-bars-mode)
(add-hook 'yaml-ts-mode-hook #'indent-bars-mode)

(setq
 indent-bars-color '(highlight :face-bg t :blend 0.15)
 indent-bars-pattern "."
 indent-bars-width-frac 0.1
 indent-bars-pad-frac 0.1
 indent-bars-zigzag nil
 indent-bars-color-by-depth nil
 indent-bars-display-on-blank-lines nil)

(setq indent-bars-treesit-support (treesit-available-p)
      ;; indent-bars-prefer-character "│"
      indent-bars-highlight-current-depth '(:face default :blend 0.3)
      ;; https://github.com/jdtsmith/indent-bars#configuring-tree-sitter
      indent-bars-treesit-ignore-blank-lines-types '("module")
      indent-bars-treesit-wrap '((python argument_list parameters
				                         list list_comprehension
				                         dictionary dictionary_comprehension
				                         parenthesized_expression subscript)))
#+end_src
*** devdocs.el
#+begin_src emacs-lisp
(eat/pkg devdocs)
(bind global-map "C-h D" #'devdocs-lookup)
#+end_src
*** drepl
#+begin_src emacs-lisp
(eat/pkg drepl)
#+end_src

** lsp-bridge
#+begin_src elisp
(eat/pkg lsp-bridge "https://github.com/manateelazycat/lsp-bridge")
(eat/pkg flymake-bridge "https://github.com/liuyinz/flymake-bridge")

(setq lsp-bridge-c-lsp-server "ccls"
      acm-enable-tabnine nil
      ;; install lsp server and run lsp_bridge.py on remote server
      lsp-bridge-enable-with-tramp t)

(defun my/lsp-bridge-mode-setup ()
  (interactive)
  (flymake-bridge-setup)
  ;; Disable corfu since lsp-bridge use acm.
  (ignore-errors
    (company-mode -1))
  ;; Use tab to jump to next field but do complete when there's acm complete.
  (with-eval-after-load 'yasnippet
    (define-key yas-keymap (kbd "<tab>") 'acm-complete-or-expand-yas-snippet)
    (define-key yas-keymap (kbd "TAB") 'acm-complete-or-expand-yas-snippet)))

(with-eval-after-load 'lsp-bridge
  (add-hook 'lsp-bridge-mode-hook #'my/lsp-bridge-mode-setup)

  (bind
   (lsp-bridge-mode-map
    "M-."     #'lsp-bridge-find-def
    "C-x 4 ." #'lsp-bridge-find-def-other-window
    "M-,"     #'lsp-bridge-find-def-return
    "M-?"     #'lsp-bridge-find-references
    "M-'"     #'lsp-bridge-find-impl
    "C-c r"   #'lsp-bridge-rename
    "M-RET"   #'lsp-bridge-code-action)
   (lsp-bridge-ref-mode-map
    "j" nil
    "k" nil
    "h" nil
    "l" nil
    "p" #'lsp-bridge-ref-jump-prev-file
    "h" #'lsp-bridge-ref-jump-prev-keyword
    "t" #'lsp-bridge-ref-jump-next-keyword
    "n" #'lsp-bridge-ref-jump-next-file)))
#+end_src

#+begin_src elisp
(eat/pkg eglot-hierarchy "https://github.com/dolmens/eglot-hierarchy")
#+end_src
** citre
#+begin_src elisp
(eat/pkg citre)

(keymap-global-set "C-x c j" #'citre-jump)
(keymap-global-set "C-x c u" #'citre-update-this-tags-file)
(keymap-global-set "C-x c p" #'citre-peek)
(keymap-global-set "C-x c U" #'citre-global-update-database)
(keymap-global-set "C-x c r" #'citre-jump-to-reference)

(with-eval-after-load 'citre
  (keymap-global-set "C-x c J" #'citre-jump-back)
  ;; NOTE
  ;; Notice that GTAGSOBJDIRPREFIX must exist for gtags to use it. So you need to run:
  ;; $ mkdir -p ~/.cache/gtags/
  (keymap-global-set "C-x c P" #'citre-ace-peek-references)
  (setq citre-default-create-tags-file-location 'global-cache
        citre-use-project-root-when-creating-tags t
        citre-prompt-language-for-ctags-command t
        citre-auto-enable-citre-mode-modes '(prog-mode))
  (with-eval-after-load 'cc-mode (require 'citre-lang-c))
  (with-eval-after-load 'dired (require 'citre-lang-fileref))
  (with-eval-after-load 'verilog-mode (require 'citre-lang-verilog)))

(with-eval-after-load 'citre-global
  (setenv "GTAGSOBJDIRPREFIX" (concat (getenv "HOME") "/.cache/gtags"))
  (setenv "GTAGSCONF" (concat (getenv "HOME") "/.globalrc"))
  (setenv "GTAGSLABEL" "native-pygments"))

(with-eval-after-load 'citre-peek
  (keymap-set citre-peek-keymap "M-l r" 'citre-peek-through-references))
#+end_src
** Lang
*** lisp
#+begin_src elisp
(eat/pkg aggressive-indent)

(add-hook 'emacs-lisp-mode-hook #'aggressive-indent-mode)
(add-hook 'lisp-interaction-mode-hook #'aggressive-indent-mode)
(add-hook 'scheme-mode-hook #'aggressive-indent-mode)
(add-hook 'lisp-mode-hook #'aggressive-indent-mode)
#+end_src
*** go
#+begin_src emacs-lisp
(eat/pkg go-mode)
(eat/pkg flymake-go-staticcheck)
(eat/pkg go-gen-test)
(eat/pkg go-tag)
(eat/pkg go-dlv)
(eat/pkg go-fill-struct)
(eat/pkg go-impl)
(eat/pkg gotest)

;; Install or update tools
(defvar go--tools '("golang.org/x/tools/gopls"
                    "golang.org/x/tools/cmd/goimports"
                    "honnef.co/go/tools/cmd/staticcheck"
                    "github.com/go-delve/delve/cmd/dlv"
                    "github.com/zmb3/gogetdoc"
                    "github.com/josharian/impl"
                    "github.com/cweill/gotests/..."
                    "github.com/fatih/gomodifytags"
                    "github.com/davidrjenni/reftools/cmd/fillstruct"
                    "github.com/rogpeppe/godef")
  "All necessary go tools.")

(defun go-update-tools ()
  "Install or update go tools."
  (interactive)
  (unless (executable-find "go")
    (user-error "Unable to find `go' in `exec-path'!"))

  (message "Installing go tools...")
  (dolist (pkg go--tools)
    (set-process-sentinel
     (start-process "go-tools" "*Go Tools*" "go" "install" "-v" "-x" (concat pkg "@latest"))
     (lambda (proc _)
       (let ((status (process-exit-status proc)))
         (if (= 0 status)
             (message "Installed %s" pkg)
           (message "Failed to install %s: %d" pkg status)))))))
(setq gofmt-command "goimports"
      gofmt-show-errors nil)
(add-hook 'go-test-mode-hook #'visual-line-mode)

;; Go-test
(setq go-test-verbose t
      ;; Do not cache test result.
      go-test-args "-count=1")
;; Go-tag
(setq go-tag-args (list "-transform" "camelcase"))
;; Bind
(with-eval-after-load 'go-mode
  (bind go-mode-map
        "C-c t g" #'go-gen-test-dwim
        "C-c t t" #'go-test-current-test
        "C-c t a" #'go-tag-add
        "C-c t r" #'go-tag-remove))
#+end_src
*** nix
Install =rnix-lsp= and =nixfmt=.
#+begin_src emacs-lisp
(eat/pkg nix-mode)
#+end_src

*** clojure
#+begin_src elisp
(eat/pkg clojure-mode)
(eat/pkg cider)
(eat/pkg clj-refactor)

(add-hook 'clojure-mode-hook #'puni-mode)

(with-eval-after-load 'clojure-mode
  ;; better indentation for compojure
  ;; https://github.com/weavejester/compojure/wiki/Emacs-indentation
  (define-clojure-indent
   (defroutes 'defun)
   (GET 2)
   (POST 2)
   (PUT 2)
   (DELETE 2)
   (HEAD 2)
   (ANY 2)
   (OPTIONS 2)
   (PATCH 2)
   (rfn 2)
   (let-routes 1)
   (context 2)))
#+end_src
*** c
#+begin_src elisp
(setq c-default-style "linux"
      c-basic-offset 4)
#+end_src
*** python
#+begin_src elisp
(setq python-indent-offset 4
      python-shell-completion-native-enable nil
      python-indent-guess-indent-offset nil)

(eat/pkg pet)
;; This will turn on `pet-mode' on `python-mode' and `python-ts-mode'
(add-hook 'python-base-mode-hook 'pet-mode -10)
#+end_src
*** sql
#+begin_src elisp
(setq sql-mysql-login-params '(user password server database port))
#+end_src
*** javascript
#+begin_src emacs-lisp
;; NOTE js-ts-mode and tsx-ts-mode have bug to stuck emacs
(setq-default js-indent-level 2)
#+end_src
*** sbcl
#+begin_src emacs-lisp
(eat/pkg sly)
(eat/pkg slime)
(setq inferior-lisp-program "sbcl")
#+end_src
** dape, DAP Debugger
#+begin_src emacs-lisp
(eat/pkg dape)
(autoload #'dape-toggle-breakpoint "dape" nil t)
#+end_src
* Org mode

#+begin_src emacs-lisp
(setq
 org-directory (expand-file-name "~/Dropbox/org")
 calendar-date-style 'ios ;; YYYY-MM-DD
 org-plantuml-exec-mode 'plantuml
 org-complete-tags-always-offer-all-agenda-tags t
 org-footnote-auto-adjust t
 org-footnote-section nil ;; Footnotes go into the section they are referenced in
 org-hide-emphasis-markers t
 org-return-follows-link t
 org-image-actual-width '(300)
 org-ellipsis " ▾ "
 org-special-ctrl-a/e t
 org-special-ctrl-k t
 org-modules nil
 org-log-done t
 org-confirm-babel-evaluate nil
 org-edit-src-content-indentation 0
 org-src-window-setup 'current-window)

(defun eat/org-mode-setup ()
  (auto-fill-mode 1)
  (org-indent-mode 1)
  (electric-pair-local-mode -1)
  (electric-quote-local-mode)
  (electric-indent-local-mode -1))
(add-hook 'org-mode-hook #'eat/org-mode-setup)

(with-eval-after-load 'org
  (add-to-list 'org-structure-template-alist '("el" . "src emacs-lisp"))
  (require 'org-tempo))
#+end_src

** valign

#+begin_src elisp
(eat/pkg valign)
(setq valign-fancy-bar t)
(when (display-graphic-p)
  (add-hook 'org-mode-hook #'valign-mode)
  (add-hook 'markdown-mode-hook #'valign-mode))
#+end_src

** More workflow states

#+begin_src emacs-lisp
(setq org-todo-keywords '((sequence "TODO(t)" "WIP(i!)" "WAIT(w!)" "|" "DONE(d!)" "CANCELLED(c@/!)"))
      org-todo-keyword-faces '(("TODO"       :foreground "#7c7c75" :weight bold)
                               ("WIP"        :foreground "#0098dd" :weight bold)
                               ("WAIT"       :foreground "#9f7efe" :weight bold)
                               ("DONE"       :foreground "#50a14f" :weight bold)
                               ("CANCELLED"  :foreground "#ff6480" :weight bold)))
#+end_src

** Save when I change a workflow state

#+begin_src emacs-lisp
(add-hook 'org-trigger-hook 'save-buffer)
#+end_src

** LaTex

#+begin_src emacs-lisp
(setq org-latex-compiler "xelatex")
(setq org-preview-latex-default-process 'dvisvgm)
(setq org-preview-latex-image-directory "~/.cache/org-latex")
(setq org-latex-pdf-process
      '("xelatex -shell-escape -interaction nonstopmode -output-directory %o %f"
        "xelatex -shell-escape -interaction nonstopmode -output-directory %o %f"
        "xelatex -shell-escape -interaction nonstopmode -output-directory %o %f"
        "rm -fr %b.out %b.log %b.tex auto"))
(setq org-latex-packages-alist '("\\usepackage[UTF8, fontset=fandol]{ctex}"))
#+end_src

** Capture

#+begin_src elisp
(keymap-global-set "C-c c" 'org-capture)

(setq org-default-notes-file (concat org-directory "/default-notes.org")
      org-capture-templates
      `(("b" "Blog idea" entry (file "~/Dropbox/org/blog.org") "* %^{title}\n%u\n%?" :prepend t)
        ("p" "Project idea" entry (file "~/Dropbox/org/project.org") "* %^{title}\n%u\n%?" :prepend t)
        ("i" "Inbox" entry (file "~/Dropbox/org/inbox.org") "* TODO %?\n:PROPERITIES:\n:Created: %T\n:END:")
        ("n" "Note" entry (file "~/Dropbox/org/roma/Notes.org") "* %^{title}\n%u\n%?" :prepend t)
        ("w" "Work" entry (file+olp+datetree "~/Dropbox/org/Work.org")
         "* %^{Title}\n:PROPERITIES:\n:Created: %T\n:END:" :tree-type week)))
#+end_src

** Agenda

#+begin_src elisp
(setq org-agenda-files (list org-directory)
      org-agenda-prefix-format '((agenda . " %i %-12:c%?-12t% s")
                                 (todo   . " ")
                                 (tags   . " %i %-12:c")
                                 (search . " %i %-12:c"))
      ;; hide any tag
      org-agenda-hide-tags-regexp "."
      org-agenda-current-time-string
      "⭠ now ─────────────────────────────────────────────────")

(keymap-global-set "C-c a" 'org-agenda)
#+end_src

** 零宽空格，中文排版

没有=零宽=空格
有​=零宽=​空格
使用 prettify 显示零宽空格

#+begin_src emacs-lisp
(defun eat/insert-zero-width-space ()
  (interactive)
  (insert-char ?\u200B))
(keymap-global-set "M-SPC" #'eat/insert-zero-width-space)

;; 导出时（导出为 org 时除外），去除零宽空格
(defun +org-export-remove-zero-width-space (text _backend _info)
  "Remove zero width spaces from TEXT."
  (unless (org-export-derived-backend-p 'org)
    (replace-regexp-in-string "\u200b" "" text)))
(with-eval-after-load 'ox
  (add-to-list 'org-export-filter-final-output-functions #'+org-export-remove-zero-width-space t))
#+end_src

** org-appear

#+begin_src emacs-lisp
(eat/pkg org-appear)
(add-hook 'org-mode-hook #'org-appear-mode)
#+end_src

** org-variable-pitch

#+begin_src emacs-lisp
(eat/pkg org-variable-pitch)
#+end_src

** org-modern

#+begin_src emacs-lisp
(eat/pkg org-modern)
(eat/pkg org-modern-indent "https://github.com/jdtsmith/org-modern-indent")

(setq org-modern-star ["›"]
      ;; Enable this will break code block indentation.
      org-modern-block-fringe nil)

;; org modern indent
(setq org-modern-hide-stars nil
      org-modern-block-name '("" . ""))

;; valign
(setq org-modern-table nil)

(defun eat/setup-org-modern ()
  (setq-local line-spacing 0.15)
  (org-modern-mode))
(add-hook 'org-mode-hook 'eat/setup-org-modern)
(add-hook 'org-agenda-finalize-hook #'org-modern-agenda)
(add-hook 'org-mode-hook 'org-modern-indent-mode 90)
#+end_src

** Org babel

#+begin_src emacs-lisp
(add-hook 'org-babel-after-execute #'org-redisplay-inline-images)
(eat/pkg ob-restclient)
(eat/pkg ob-go)
#+end_src

** Org export

For now use ~pandoc --embed-resources --standalone~.

#+begin_src emacs-lisp
(eat/pkg htmlize)
(defun eat/org-export-to-html ()
  "Convert current org buffer to html with image embed.
Need pandoc installed."
  (interactive)
  (let* ((from (buffer-file-name))
         (to (concat (file-name-sans-extension from) ".html")))
    (shell-command (format "pandoc --embed-resources --standalone %s -o %s" from to))
    (find-file to)))

(eat/pkg ox-gfm)
(with-eval-after-load 'org
  (add-to-list 'org-export-backends 'md))
#+end_src

** toc-org

#+begin_src elisp
(eat/pkg toc-org)
(autoload 'toc-org-enable "toc-org" nil t)
(autoload 'toc-org-insert-toc "toc-org" nil t)
#+end_src

** citar

M-x ~org-cite-insert~

#+begin_src elisp
(eat/pkg citar)

(setq org-cite-global-bibliography '("~/Dropbox/bib/references.bib")
      org-cite-insert-processor 'citar
      org-cite-follow-processor 'citar
      org-cite-activate-processor 'citar
      citar-bibliography org-cite-global-bibliography)

#+end_src

** org-static-blog

#+begin_src elisp
(eat/pkg org-static-blog)
(setq org-static-blog-publish-title "mrunhap's blog"
      org-static-blog-publish-url "https://mrunhap.github.io/"
      org-static-blog-publish-directory "~/p/blog/"
      org-static-blog-posts-directory "~/p/blog/posts/"
      org-static-blog-drafts-directory "~/p/blog/drafts/"
      org-static-blog-enable-tags t
      org-static-blog-use-preview t
      org-static-blog-preview-ellipsis ""
      org-export-with-toc nil
      org-export-with-section-numbers nil)
(with-eval-after-load 'org-static-blog
  (setq org-static-blog-page-header (get-string-from-file "~/p/blog/static/header.html")
        org-static-blog-page-preamble (get-string-from-file "~/p/blog/static/preamble.html")
        org-static-blog-page-postamble (get-string-from-file "~/p/blog/static/postamble.html")))
#+end_src

* Markdown

#+begin_src emacs-lisp
(eat/pkg markdown-mode)
(eat/pkg markdown-toc)

(setq markdown-enable-wiki-links t
      markdown-italic-underscore t
      markdown-asymmetric-header t
      markdown-make-gfm-checkboxes-buttons t
      markdown-gfm-uppercase-checkbox t
      markdown-fontify-code-blocks-natively t)

(add-to-list 'auto-mode-alist '("README\\.md\\'" . gfm-mode))

(defun eat/markdown-mode-setup ()
  (when (executable-find "ltex-ls")
    (eglot-ensure)))
(add-hook 'markdown-mode-hook 'eat/markdown-mode-setup)
#+end_src

* LaTex

#+begin_src emacs-lisp
(defun eat/latex-mode-setup ()
  (when (executable-find "digestif")
    (company-mode 1)
    (eglot-ensure)))
(add-hook 'latex-mode-hook 'eat/latex-mode-setup)
#+end_src

* Typst

#+begin_src emacs-lisp
(eat/pkg typst-ts-mode "https://git.sr.ht/~meow_king/typst-ts-mode")
#+end_src

* Magit

#+begin_src emacs-lisp
(eat/pkg magit)
(add-hook 'magit-diff-visit-file #'my-recenter-and-pulse-line)
(keymap-global-set "C-x g" #'magit-status)
#+end_src

** magit-delta

#+begin_src emacs-lisp
(eat/pkg magit-delta)
(when (executable-find "delta")
  (add-hook 'magit-mode-hook #'magit-delta-mode))
#+end_src

** diff-hl

#+begin_src emacs-lisp
(eat/pkg diff-hl)
(add-hook 'prog-mode-hook #'diff-hl-mode)
(add-hook 'conf-mode-hook #'diff-hl-mode)
(add-hook 'dired-mode-hook #'diff-hl-dired-mode)

(setq diff-hl-draw-borders nil
      diff-hl-disable-on-remote t)

(with-eval-after-load 'diff-hl
  (add-hook 'magit-pre-refresh-hook #'diff-hl-magit-pre-refresh)
  (add-hook 'magit-post-refresh-hook #'diff-hl-magit-post-refresh)
  ;; Highlight on-the-fly
  (diff-hl-flydiff-mode 1)
  (unless (display-graphic-p)
    ;; Fall back to the display margin since the fringe is unavailable in tty
    (diff-hl-margin-mode 1)
    ;; Avoid restoring `diff-hl-margin-mode'
    (with-eval-after-load 'desktop
      (add-to-list 'desktop-minor-mode-table
                   '(diff-hl-margin-mode nil)))))
#+end_src

* Dirvish

#+begin_src elisp
(eat/pkg dirvish)

(add-hook 'after-init-hook #'dirvish-override-dired-mode)

(setq dirvish-attributes '(vc-state subtree-state nerd-icons)
      dirvish-header-line-height 20
      dirvish-mode-line-height 20)

(keymap-global-set "<f1>" #'dirvish-side)

(with-eval-after-load 'dirvish
  ;; (dirvish-side-follow-mode) ;; FIXME
  (bind dirvish-mode-map
        "TAB" #'dirvish-subtree-toggle
        "<tab>" #'dirvish-subtree-toggle
        "a" #'dirvish-quick-access
        "f" #'dirvish-file-info-menu
        "y" #'dirvish-yank-menu
        "N" #'dirvish-narrow
        "H" #'dirvish-history-jump
        "s" #'dirvish-quicksort
        "v" #'dirvish-vc-menu
        "M-f" #'dirvish-history-go-forward
        "M-b" #'dirvish-history-go-backward
        "M-l" #'dirvish-ls-switches
        "M-m" #'dirvish-mark-menu
        "M-t" #'dirvish-layout-toggle
        "M-s" #'dirvish-setup-menu
        "M-e" #'dirvish-emerge-menu
        "M-j" #'dirvish-fd-jump
        "<mouse-1>" #'dirvish-subtree-toggle-or-open
        "<mouse-2>" #'dired-mouse-find-file-other-window
        "<mouse-3>" #'dired-mouse-find-file))
#+end_src

* Eat

#+begin_src elisp
(eat/pkg eat)
;; https://abode.karthinks.com/share/eat-modes.png

(bind global-map "C-`" #'eat)
(with-eval-after-load 'eat
  (bind eat-mode-map "C-`" #'eat/quit))

(setq eat-kill-buffer-on-exit t
      eat-enable-directory-tracking t)

(add-hook 'eshell-load-hook 'eat-eshell-mode)
(add-hook 'eshell-load-hook 'eat-eshell-visual-command-mode)
#+end_src

* Kubel

#+begin_src emacs-lisp
(eat/pkg kubel)
#+end_src

* Telega

Need =telegram-tdlib=.
Run =docker pull zevlg/telega-server:latest=

#+begin_src elisp
(eat/pkg telega)
(setq
 telega-use-docker (executable-find "docker")
 telega-server-libs-prefix (cond (eat/macp "/usr/local")
                                 (t "/usr"))
 telega-chat-fill-column 75
 ;; 头像再也不裂了
 telega-avatar-workaround-gaps-for '(return t)
 ;; 默认翻译目标语言为中文
 telega-translate-to-language-by-default "zh"
 ;; send code in markdown format
 telega-chat-input-markups '("markdown2" "org")
 ;; 使用 capf 代替 telega 默认的 ido 补全
 telega-completing-read-function completing-read-function
 ;; 省略消息中过长的 url
 telega-url-shorten-regexps (list `(too-long-link
                                    :regexp "^\\(https?://\\)\\(.\\{55\\}\\).*?$"
                                    :symbol ""
                                    :replace "\\1\\2...")))

(add-hook 'telega-root-mode-hook 'hl-line-mode)
(add-hook 'telega-chat-mode-hook 'company-mode)

(with-eval-after-load 'telega
  (add-hook 'telega-load-hook #'telega-notifications-mode)
  (add-hook 'telega-load-hook #'telega-appindicator-mode)

  (setq telega-chat-input-format "›"
        telega-animation-play-inline nil
        telega-video-play-inline nil
        ;; make sticker larger to read
        telega-sticker-size '(10 . 24)
        ;; change reply symbol
        telega-symbol-reply "↫"
        ;; set date format for old messages
        telega-old-date-format "%Y/%M/%D")

  ;; syntax highlighting in telega code
  (require 'telega-mnz)
  (global-telega-mnz-mode 1))
#+end_src

* Modes
** csv-mode

#+begin_src emacs-lisp
(eat/pkg csv-mode)

(defun my/csv-mode-setup ()
  (setq-local auto-hscroll-mode t))
(add-hook 'csv-mode-hook #'my/csv-mode-setup)
#+end_src

** protobuf-mode

#+begin_src emacs-lisp
(eat/pkg protobuf-mode)

(with-eval-after-load "protobuf-mode"
  (add-hook 'protobuf-mode-hook
            (lambda ()
              (setq imenu-generic-expression
                    '((nil "^[[:space:]]*\\(message\\|service\\|enum\\)[[:space:]]+\\([[:alnum:]]+\\)" 2))))))
#+end_src

** git-mode

#+begin_src emacs-lisp
(eat/pkg git-modes)
(add-to-list 'auto-mode-alist
             (cons "/.dockerignore\\'" 'gitignore-mode))
#+end_src

* Site Lisp
** Load Path

#+begin_src elisp
(add-to-list 'load-path (expand-file-name "site-lisp" user-emacs-directory))
(add-to-list 'custom-theme-load-path (expand-file-name "themes" user-emacs-directory))
#+end_src

** color-outline

#+begin_src elisp
(autoload #'color-outline-mode "color-outline")
(add-hook 'prog-mode-hook #'color-outline-mode)
#+end_src

** iimg

#+begin_src elisp
(autoload #'iimg-enable "iimg")
(add-hook 'text-mode-hook #'iimg-enable)
(setq iimg-prune-slices-p nil)
#+end_src

** bklink

#+begin_src elisp
(autoload #'bklink-minor-mode "bklink")

(setq bklink-summary-read-only-p t
      bklink-prune-summary-p nil)

(with-eval-after-load 'bklink
  (bind bklink-minor-mode-map
        "C-c l" #'bklink-summary-mode
        "C-c i" #'bklink-insert))
#+end_src

** quanjiao

#+begin_src emacs-lisp
(autoload #'quanjiao-mode "quanjiao" nil t)
(add-hook 'text-mode-hook 'quanjiao-mode)
#+end_src

** dired+

#+begin_src emacs-lisp
(autoload 'dired-toggle-mark-click "dired+" nil t)
(autoload 'dired-copy "dired+" nil t)
(autoload 'dired-paste "dired+" nil t)
(autoload 'dired-move "dired+" nil t)

(with-eval-after-load "dired"
  (bind dired-mode-map
        "<s-mouse-1>" #'dired-toggle-mark-click))
#+end_src
